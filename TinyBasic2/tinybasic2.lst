   1:			;Modified Nov 1 2016 by Donn Stewart for use in CPUville Z80 computer
   2:			;Changed UART (ACIA) port numbers to 3 for status, 2 for data in INIT, CHKIO, OUTC
   3:			;Status bit for read in CHKIO changed to 0x02
   4:			;Status bit for write in OUTC (actually OC3) changed to 0x01
   5:			;Changed UART initialization parameters in INIT
   6:			;Changed ORG statements at end of file to match system with 2K RAM
   7:			;
   8:			;2024-10-11 Ho-Ro:
   9:			;Automatically converted from 8080 syntax to Z80 syntax:
  10:			;https://hc-ddr.hucki.net/wiki/doku.php/cpm/8080_z80
  11:			;Assembler: uz80as for Z80 as well as i8080 (uz80as --target=i8080)
  12:			;Modified for use with Z80 dongle simulator
  13:			;UART data port 1
  14:			;UART status port 2
  15:			;New:
  16:			;Case insensitive input
  17:			;PRINT modifier for hex out: PRINT %16,..
  18:			;Hex numbers: $xxxx
  19:			;2024-10-13 Ho-Ro:
  20:			;build ROM version (2K ROM / 6.5K RAM) and RAM version (2K prog RAM / 2K free RAM)
  21:			;add command "HALT" (halts Z80, returns to dongle analyser program)
  22:			;2024-10-15 Ho-Ro:
  23:			;PRINT modifier %nn switches to unsigned number format, e.g.:
  24:			;PRINT %10,$FFFF -> 65535
  25:			;2024-10-17 Ho-Ro:
  26:			;PUT ADDR, VAL, VAL, VAL,...
  27:			;constants RAM (TXTBGN), TOP (TXTEND) and SIZE (TXTEND-TXTUNF)
  28:			;function USR(para) that calls machine code at TOP (128 bytes free)
  29:			;with parameter in HL, returning the result in HL, default is RET at TOP
  30:			;Changed to the more authentic zmac syntax (https://github.com/gp48k/zmac)
  31:			;
  32:			;*************************************************************
  33:			;
  34:			;                 TINY BASIC FOR INTEL 8080
  35:			;                       VERSION 2.0
  36:			;                     BY LI-CHEN WANG
  37:			;                  MODIFIED AND TRANSLATED
  38:			;                    TO INTEL MNEMONICS
  39:			;                     BY ROGER RAUSKOLB
  40:			;                      10 OCTOBER,1976
  41:			;                        @COPYLEFT
  42:			;                   ALL WRONGS RESERVED
  43:			;
  44:			;*************************************************************
  45:			;
  46:			; *** ZERO PAGE SUBROUTINES ***
  47:			;
  48:			; THE 8080 INSTRUCTION SET LETS YOU HAVE 8 ROUTINES IN LOW
  49:			; MEMORY THAT MAY BE CALLED BY RST N, N BEING 0 THROUGH 7.
  50:			; THIS IS A ONE BYTE INSTRUCTION AND HAS THE SAME POWER AS
  51:			; THE THREE BYTE INSTRUCTION CALL LLHH.  TINY BASIC WILL
  52:			; USE RST 0 AS START AND RST 1 THROUGH RST 7 FOR
  53:			; THE SEVEN MOST FREQUENTLY USED SUBROUTINES.
  54:			; TWO OTHER SUBROUTINES (CRLF AND TSTNUM) ARE ALSO IN THIS
  55:			; SECTION.  THEY CAN BE REACHED ONLY BY 3-BYTE CALLS.
  56:			;
  57:			
  58:			                .Z80
  59:			
  60:			; Memory map
  61:	0000          	ROMBGN:         .EQU    $0000           ; Execution must start here
  62:	0800          	RAMBGN:         .EQU    $0800           ; 2K ROM
  63:			
  64:	0000          	.IFDEF           MAKE_ROM
  67:			.ELSE
  68:			; 2K CODE IN RAM & 2K DATA IN RAM AS TEST VERSION
  69:	0800          	RAMSZE:         .EQU    $0800
  70:			.ENDIF
  71:			
  72:			; IO map
  73:	0001          	IODATA:         .EQU    1
  74:	0002          	IOSTAT:         .EQU    2
  75:	0001          	IO_RX_BIT:      .EQU    $01
  76:			
  77:			; Control character
  78:	0008          	BS:             .EQU    $08             ; ^H, BACKSPACE
  79:	000D          	CR:             .EQU    $0D             ; ^M, CARRIAGE RETURN
  80:	000A          	LF:             .EQU    $0A             ; ^J, LINE FEED
  81:	0018          	CAN:            .EQU    $18             ; ^X, CANCEL
  82:	007F          	DEL:            .EQU    $7F             ; DELETE
  83:			
  84:			
  85:			; Store a label address as BIG endian with bit A15 set
  86:			
  87:			DWA             MACRO   LABEL
  88:			                .DB     (LABEL >> 8) | $80
  89:			                .DB     LABEL & $FF
  90:			                ENDM
  91:			
  92:			; if CHAR = A THEN JUMP RELATIVE TO LABEL
  93:			
  94:			TSTC            MACRO   CHAR,LABEL
  95:			                RST     RTSTC
  96:			                .DB     CHAR
  97:			                .DB     LABEL-$-1
  98:			                ENDM
  99:			
 100:			
 101:	0000          	                .ORG    ROMBGN
 102:			
 103:	0000  310009  	CSTART:         LD      SP,STACK ;*** COLD START ***
 104:	0003  3EC9    	                LD      A,$C9   ;"RET", also not an ASCII char
 105:	0005  C30901  	                JP      INIT    ;as delimiter for PRTSTG
 106:			
 107:			                ;*** RST 1 @ $0008 ***
 108:	0008  E3      	RTSTC:          EX      (SP),HL
 109:	0009  EF      	                RST     RIGNBLK ;IGNORE BLANKS AND
 110:	000A  BE      	                CP      (HL)    ;TEST CHARACTER
 111:	000B  C37000  	                JP      TC1     ;REST OF THIS IS AT TC1
 112:			
 113:	000E  3E0D    	CRLF:           LD      A,CR    ;*** CRLF ***
 114:			;
 115:			                ;*** RST 2 @ $0010 ***
 116:	0010  D301    	ROUTC:          OUT     (IODATA),A      ;Out to data port
 117:	0012  FE0D    	                CP      CR      ;WAS IT CR?
 118:	0014  C0      	                RET     NZ      ;NO, FINISHED
 119:	0015  C3F500  	                JP      OC1     ;REST OF THIS IS AT OC1
 120:			
 121:			                ;*** RST 3 @ $0018 ***
 122:	0018  CDDA03  	REXPR:          CALL    EXPR2
 123:	001B  E5      	                PUSH    HL      ;EVALUATE AN EXPRESSION
 124:	001C  C39603  	                JP      EXPR1   ;REST OF IT AT EXPR1
 125:	001F  57      	                .DB     "W"
 126:			
 127:			                ;*** RST 4 @ $0020 ***
 128:	0020  7C      	RCOMP:          LD      A,H
 129:	0021  BA      	                CP      D       ;COMPARE HL WITH DE
 130:	0022  C0      	                RET     NZ      ;RETURN CORRECT C AND
 131:	0023  7D      	                LD      A,L     ;Z FLAGS
 132:	0024  BB      	                CP      E       ;BUT OLD A IS LOST
 133:	0025  C9      	                RET
 134:	0026  414E    	                .DB     "AN"
 135:			
 136:			                ;*** RST 5 @ $0028 ***
 137:	0028  1A      	RIGNBLK:        LD      A,(DE)
 138:	0029  FE20    	                CP      20H     ;IGNORE BLANKS
 139:	002B  C0      	                RET     NZ      ;IN TEXT (WHERE DE->)
 140:	002C  13      	                INC     DE      ;AND RETURN THE FIRST
 141:	002D  C32800  	                JP      RIGNBLK  ;NON-BLANK CHAR. IN A
 142:			
 143:			                ;*** RST 6 @ $0030 ***
 144:	0030  F1      	RFINISH:        POP     AF
 145:	0031  CD4005  	                CALL    FIN     ;CHECK END OF COMMAND
 146:	0034  C35305  	                JP      QWHAT   ;PRINT "WHAT?" IF WRONG
 147:	0037  47      	                .DB     "G"
 148:			
 149:			                ;*** RST 7 @ $0038 ***
 150:	0038  EF      	RTSTV:          RST     RIGNBLK ;IGNBLK
 151:	0039  D640    	                SUB     '@'     ;TEST VARIABLES
 152:	003B  D8      	                RET     C       ;C: < '@', NOT A VARIABLE
 153:	003C  C25A00  	                JP      NZ,TV1  ;NZ: NOT THE '@' ARRAY
 154:			;
 155:	003F  13      	                INC     DE      ;IT IS THE "@" ARRAY
 156:	0040  CD7B04  	                CALL    PARN    ;@ SHOULD BE FOLLOWED
 157:	0043  29      	                ADD     HL,HL   ;BY (EXPR) AS ITS INDEX
 158:	0044  DAD000  	                JP      C,QHOW  ;IS INDEX TOO BIG (>0x7FFF)?
 159:	0047  23      	                INC     HL      ;ADD TWO BYTES
 160:	0048  23      	                INC     HL      ;FOR @(0)
 161:	0049  D5      	                PUSH    DE      ;WILL IT OVERWRITE
 162:	004A  EB      	                EX      DE,HL   ;TEXT?
 163:	004B  CDBD04  	                CALL    SIZE    ;FIND SIZE OF FREE RAM
 164:	004E  E7      	                RST     RCOMP   ;AND CHECK THAT
 165:	004F  DA8105  	                JP      C,ASORRY  ;IF SO, SAY "SORRY"
 166:	0052  21000F  	                LD      HL,TXTEND ;IF NOT GET ADDRESS
 167:	0055  CD0D05  	                CALL    SUBDE   ;OF @(EXPR) AND PUT IT
 168:	0058  D1      	                POP     DE      ;IN HL (top-down from TXTEND)
 169:	0059  C9      	                RET             ;C FLAG IS CLEARED
 170:			;
 171:			                ; VARIABLES 'A'..'Z'
 172:	005A  FE21    	TV1:            CP      21H     ;>='a'?
 173:	005C  3802    	                JR      C,TV2   ;NO
 174:	005E  D620    	                SUB     20H     ;MAKE UPPER CASE
 175:	0060  FE1B    	TV2:            CP      1BH     ;<='Z'
 176:	0062  3F      	                CCF             ;IF NOT RETURN C FLAG
 177:	0063  D8      	                RET     C
 178:	0064  13      	                INC     DE      ;IT IS 'A'=1 THROUGH 'Z'=26
 179:	0065  21FE07  	                LD      HL,VARBGN-2       ;COMPUTE ADDRESS OF
 180:	0068  07      	                RLCA            ;THAT VARIABLE
 181:	0069  85      	                ADD     A,L     ;AND RETURN IT IN HL
 182:	006A  6F      	                LD      L,A     ;WITH C FLAG CLEARED
 183:	006B  3E00    	                LD      A,0
 184:	006D  8C      	                ADC     A,H
 185:	006E  67      	                LD      H,A
 186:	006F  C9      	                RET
 187:			
 188:			;TSTC:          EX      (SP),HL ;*** TSTC OR RST 1 ***
 189:			;               RST  RIGNBLK    ;THIS IS AT LOC. 8
 190:			;               CMP     (HL)    ;AND THEN JUMP HERE
 191:			;               JP      TC1     ;REST OF THIS IS AT TC1
 192:	0070  23      	TC1:            INC     HL      ;COMPARE THE BYTE THAT
 193:	0071  2807    	                JR      Z,TC2   ;FOLLOWS THE RST INST.
 194:	0073  C5      	                PUSH    BC      ;WITH THE TEXT (DE->)
 195:	0074  4E      	                LD      C,(HL)  ;IF NOT =, ADD THE 2ND
 196:	0075  0600    	                LD      B,0     ;BYTE THAT FOLLOWS THE
 197:	0077  09      	                ADD     HL,BC   ;RST TO THE OLD PC
 198:	0078  C1      	                POP     BC      ;I.E., DO A RELATIVE
 199:	0079  1B      	                DEC     DE      ;JUMP IF NOT =
 200:	007A  13      	TC2:            INC     DE      ;IF =, SKIP THOSE BYTES
 201:	007B  23      	                INC     HL      ;AND CONTINUE
 202:	007C  E3      	                EX      (SP),HL
 203:	007D  C9      	                RET
 204:			
 205:	007E  210000  	TSTNUM:         LD      HL,0    ;*** TSTNUM ***
 206:	0081  44      	                LD      B,H     ;TEST IF THE TEXT IS
 207:	0082  EF      	                RST     RIGNBLK ;A NUMBER
 208:	0083  FE24    	                CP      '$'     ;HEX NUMBER?
 209:	0085  2822    	                JR      Z,TX1   ;YES
 210:	0087  FE30    	TN1:            CP      '0'     ;IF NOT, RETURN 0 IN
 211:	0089  D8      	                RET     C       ;B AND HL
 212:	008A  FE3A    	                CP      '9'+1   ;IF NUMBERS, CONVERT
 213:	008C  D0      	                RET     NC      ;TO BINARY IN HL AND
 214:	008D  3EF0    	                LD      A,0F0H  ;SET B TO # OF DIGITS
 215:	008F  A4      	                AND     H       ;IF H>15, THERE IS NO
 216:	0090  203E    	                JR      NZ,QHOW ;ROOM FOR NEXT DIGIT
 217:	0092  04      	                INC     B       ;B COUNTS # OF DIGITS
 218:	0093  C5      	                PUSH    BC
 219:	0094  44      	                LD      B,H     ;HL=10*HL+(NEW DIGIT)
 220:	0095  4D      	                LD      C,L
 221:	0096  29      	                ADD     HL,HL   ; 2*HL
 222:	0097  29      	                ADD     HL,HL   ; 4*HL
 223:	0098  09      	                ADD     HL,BC   ; 5*HL
 224:	0099  29      	                ADD     HL,HL   ;10*HL
 225:	009A  1A      	                LD      A,(DE)  ;AND (DIGIT) IS FROM
 226:	009B  13      	                INC     DE      ;STRIPPING THE ASCII
 227:	009C  E60F    	                AND     0FH     ;CODE
 228:	009E  85      	                ADD     A,L
 229:	009F  6F      	                LD      L,A
 230:	00A0  3E00    	                LD      A,0
 231:	00A2  8C      	                ADC     A,H
 232:	00A3  67      	                LD      H,A
 233:	00A4  C1      	                POP     BC
 234:	00A5  1A      	                LD      A,(DE)  ;DO THIS DIGIT AFTER
 235:	00A6  F28700  	                JP      P,TN1   ;DIGIT. S SAYS OVERFLOW
 236:			;
 237:			                                ;OUTPUT HEX NUMBER
 238:	00A9  13      	TX1:            INC     DE      ;SKIP TO NEXT HEX
 239:	00AA  1A      	                LD      A,(DE)  ;GET HEX DIGIT
 240:	00AB  FE30    	                CP      '0'     ;< '0'
 241:	00AD  D8      	                RET     C       ;ERROR
 242:	00AE  FE3A    	                CP      '9'+1   ;<= '9'
 243:	00B0  380A    	                JR      C,TX2   ;OK '0'..'9'
 244:	00B2  FE41    	                CP      'A'     ;< 'A'
 245:	00B4  D8      	                RET     C       ;ERROR, >'9' && < 'A'
 246:	00B5  E65F    	                AND     5FH     ;CONVERT ALPHA TO UPPER
 247:	00B7  FE47    	                CP      'F'+1   ;> 'F'
 248:	00B9  D0      	                RET     NC      ;ERROR
 249:	00BA  D607    	                SUB     'A'-'0'-10      ;SKIP GAP '9' -> 'A'
 250:	00BC  E60F    	TX2:            AND     0FH     ;GET HEX CODE 0..F
 251:	00BE  C5      	                PUSH    BC
 252:	00BF  47      	                LD      B,A     ;SAVE HEX CODE
 253:	00C0  3EF0    	                LD      A,0F0H  ;IF H>15
 254:	00C2  A4      	                AND     H       ;THERE IS NO ROOM
 255:	00C3  78      	                LD      A,B
 256:	00C4  C1      	                POP     BC
 257:	00C5  2009    	                JR      NZ,QHOW ;FOR NEXT DIGIT
 258:			
 259:	00C7  04      	                INC     B       ;B COUNTS # OF DIGITS
 260:	00C8  29      	                ADD     HL,HL   ;2*HL
 261:	00C9  29      	                ADD     HL,HL   ;4*HL
 262:	00CA  29      	                ADD     HL,HL   ;8*HL
 263:	00CB  29      	                ADD     HL,HL   ;16*HL
 264:	00CC  B5      	                OR      L       ;PUT HEX CODE INTO
 265:	00CD  6F      	                LD      L,A     ;THE 4 LSB OF HL
 266:	00CE  18D9    	                JR      TX1     ;DIGIT AFTER DIGIT
 267:			
 268:	00D0  D5      	QHOW:           PUSH    DE      ;*** ERROR "HOW?" ***
 269:	00D1  11E100  	AHOW:           LD      DE,HOW
 270:	00D4  C35705  	                JP      ERROR
 271:			
 272:	00D7  54696E79	TIBAS:          .DB     "TinyBASIC"
	      42415349
	      43
 273:	00E0  0D      	                .DB     CR
 274:			
 275:	00E1  484F573F	HOW:            .DB     "HOW?"
 276:	00E5  0D      	                .DB     CR
 277:			
 278:	00E6  4F4B    	OK:             .DB     "OK"
 279:	00E8  0D      	                .DB     CR
 280:			
 281:	00E9  57484154	WHAT:           .DB     "WHAT?"
	      3F
 282:	00EE  0D      	                .DB     CR
 283:			
 284:	00EF  534F5252	SORRY:          .DB     "SORRY"
	      59
 285:	00F4  0D      	                .DB     CR
 286:			
 287:			;
 288:			;*************************************************************
 289:			;
 290:			; *** ROUTC *** CHKIO ***
 291:			;
 292:			; THESE ARE THE ONLY I/O ROUTINES IN TBI.
 293:			; OUTC WILL OUTPUT THE BYTE IN A.
 294:			; IF THAT IS A CR, A LF IS ALSO SEND OUT.
 295:			; ONLY THE FLAGS MAY BE CHANGED AT RETURN.
 296:			; ALL REGISTERS ARE RESTORED.
 297:			;
 298:			; 'CHKIO' CHECKS THE INPUT.
 299:			; IF NO INPUT, IT WILL RETURN TO THE CALLER WITH THE Z FLAG SET.
 300:			; IF THERE IS INPUT, Z FLAG IS CLEARED AND THE INPUT BYTE IS IN A.
 301:			; IF A CONTROL-C IS READ, 'CHKIO' WILL RESTART TBI
 302:			; AND DO NOT RETURN TO THE CALLER.
 303:			;
 304:			
 305:			;THIS IS AT LOC. 10
 306:			;ROUTC:         OUT     (IODATA),A      ;Out to data port
 307:			;               CP      CR      ;WAS IT CR?
 308:			;               RET     NZ      ;NO, FINISHED
 309:			;               JP      OC1     ;REST OF THIS IS AT OC1
 310:			;
 311:	00F5  3E0A    	OC1:            LD      A,LF    ;YES, WE SEND LF TOO
 312:	00F7  D7      	                RST     ROUTC   ;THIS IS RECURSIVE
 313:	00F8  3E0D    	                LD      A,CR    ;GET CR BACK IN A
 314:	00FA  C9      	                RET
 315:			
 316:	00FB  DB02    	CHKIO:          IN      A,(IOSTAT)      ;*** CHKIO ***
 317:	00FD  E601    	                AND     IO_RX_BIT       ;MASK STATUS BIT
 318:	00FF  C8      	                RET     Z       ;NOT READY, RETURN "Z"
 319:	0100  DB01    	                IN      A,(IODATA)      ;READY, READ DATA
 320:	0102  E67F    	                AND     7FH     ;MASK BIT 7 OFF
 321:	0104  FE03    	CI0:            CP      03H     ;IS IT CONTROL-C?
 322:	0106  C0      	                RET     NZ      ;NO, RETURN "NZ"
 323:	0107  1815    	                JR      WSTART  ;YES, RESTART TBI
 324:			
 325:			;
 326:			;*************************************************************
 327:			;
 328:			
 329:			;
 330:			;*************************************************************
 331:			;
 332:			; *** INIT ***
 333:			;
 334:			; PUT IO INITIALISATION HERE, E.G. FOR THE SERIAL INTERFACE
 335:			;
 336:			; *** MAIN ***
 337:			;
 338:			; THIS IS THE MAIN LOOP THAT COLLECTS THE TINY BASIC PROGRAM
 339:			; AND STORES IT IN THE MEMORY.
 340:			;
 341:			; AT START, IT PRINTS OUT "(CR)OK(CR)", AND INITIALIZES THE
 342:			; STACK AND SOME OTHER INTERNAL VARIABLES.  THEN IT PROMPTS
 343:			; ">" AND READS A LINE.  IF THE LINE STARTS WITH A NON-ZERO
 344:			; NUMBER, THIS NUMBER IS THE LINE NUMBER.  THE LINE NUMBER
 345:			; (IN 16 BIT BINARY) AND THE REST OF THE LINE (INCLUDING CR)
 346:			; IS STORED IN THE MEMORY.  IF A LINE WITH THE SAME LINE
 347:			; NUMBER IS ALREADY THERE, IT IS REPLACED BY THE NEW ONE.  IF
 348:			; THE REST OF THE LINE CONSISTS OF A CR ONLY, IT IS NOT STORED
 349:			; AND ANY EXISTING LINE WITH THE SAME LINE NUMBER IS DELETED.
 350:			;
 351:			; AFTER A LINE IS INSERTED, REPLACED, OR DELETED, THE PROGRAM
 352:			; LOOPS BACK AND ASKS FOR ANOTHER LINE.  THIS LOOP WILL BE
 353:			; TERMINATED WHEN IT READS A LINE WITH ZERO OR NO LINE
 354:			; NUMBER; AND CONTROL IS TRANSFERED TO "DIRECT".
 355:			;
 356:			; TINY BASIC PROGRAM SAVE AREA STARTS AT THE MEMORY LOCATION
 357:			; LABELED "TXTBGN" AND ENDS AT "TXTEND".  WE ALWAYS FILL THIS
 358:			; AREA STARTING AT "TXTBGN", THE UNFILLED PORTION IS POINTED
 359:			; BY THE CONTENT OF A MEMORY LOCATION LABELED "TXTUNF".
 360:			;
 361:			; THE MEMORY LOCATION "CURRNT" POINTS TO THE LINE NUMBER
 362:			; THAT IS CURRENTLY BEING INTERPRETED.  WHILE WE ARE IN
 363:			; THIS LOOP OR WHILE WE ARE INTERPRETING A DIRECT COMMAND
 364:			; (SEE NEXT SECTION). "CURRNT" SHOULD POINT TO A 0.
 365:			;
 366:			;
 367:			;THIS IS AT LOC. 0
 368:			;CSTART:        LD      SP,STACK    ;*** COLD START ***
 369:			;               LD      A,$C9       ;"RET", also != ASCII char
 370:			;               JP      INIT        ;for PRTSTG
 371:			;
 372:	0109  32000F  	INIT:           LD      (USRSPC),A  ;"RET" AT USR CODE SPACE
 373:	010C  11D700  	                LD      DE,TIBAS    ;COLD START MESSAGE
 374:	010F  CDEA05  	                CALL    PRTSTG
 375:	0112  210000  	                LD      HL,CSTART   ;INIT RANDOM POINTER
 376:	0115  224A08  	                LD      (RANPNT),HL
 377:	0118  210009  	                LD      HL,TXTBGN   ;UNFILLED TEXT
 378:	011B  223608  	                LD      (TXTUNF),HL
 379:			;
 380:	011E  310009  	WSTART:         LD      SP,STACK    ;*** WARM START ***
 381:	0121  CD0E00  	                CALL    CRLF        ;AND JUMP TO HERE
 382:	0124  11E600  	                LD      DE,OK       ;DE->STRING
 383:	0127  97      	                SUB     A           ;A=0
 384:	0128  CDEA05  	                CALL    PRTSTG      ;PRINT STRING UNTIL CR
 385:	012B  213201  	                LD      HL,ST2+1    ;HACK ST2+1 -> 0000
 386:	012E  223808  	                LD      (CURRNT),HL ;CURRENT->LINE # = 0
 387:	0131  210000  	ST2:            LD      HL,0000     ;
 388:	0134  224008  	                LD      (LOPVAR),HL
 389:	0137  223A08  	                LD      (STKGOS),HL
 390:	013A  3E3E    	ST3:            LD      A,'>'       ;PROMPT '>' AND
 391:	013C  CD8605  	                CALL    GETLN       ;READ A LINE
 392:	013F  D5      	                PUSH    DE          ;DE->END OF LINE
 393:	0140  11800F  	                LD      DE,BUFFER   ;DE->BEGINNING OF LINE
 394:	0143  CD7E00  	                CALL    TSTNUM      ;TEST IF IT IS A NUMBER
 395:	0146  EF      	                RST     RIGNBLK
 396:	0147  7C      	                LD      A,H         ;HL=VALUE OF THE # OR
 397:	0148  B5      	                OR      L           ;0 IF NO # WAS FOUND
 398:	0149  C1      	                POP     BC          ;BC->END OF LINE
 399:	014A  CAF206  	                JP      Z,DIRECT
 400:	014D  1B      	                DEC     DE          ;BACKUP DE AND SAVE
 401:	014E  7C      	                LD      A,H         ;VALUE OF LINE # THERE
 402:	014F  12      	                LD      (DE),A
 403:	0150  1B      	                DEC     DE
 404:	0151  7D      	                LD      A,L
 405:	0152  12      	                LD      (DE),A
 406:	0153  C5      	                PUSH    BC          ;BC,DE->BEGIN, END
 407:	0154  D5      	                PUSH    DE
 408:	0155  79      	                LD      A,C
 409:	0156  93      	                SUB     E
 410:	0157  F5      	                PUSH    AF          ;A=# OF BYTES IN LINE
 411:	0158  CDC505  	                CALL    FNDLN       ;FIND THIS LINE IN SAVE
 412:	015B  D5      	                PUSH    DE          ;AREA, DE->SAVE AREA
 413:	015C  2010    	                JR      NZ,ST4      ;NZ:NOT FOUND, INSERT
 414:	015E  D5      	                PUSH    DE          ;Z:FOUND, DELETE IT
 415:	015F  CDE005  	                CALL    FNDNXT      ;FIND NEXT LINE
 416:			                                    ;DE->NEXT LINE
 417:	0162  C1      	                POP     BC          ;BC->LINE TO BE DELETED
 418:	0163  2A3608  	                LD      HL,(TXTUNF) ;HL->UNFILLED SAVE AREA
 419:	0166  CD9A06  	                CALL    MVUP        ;MOVE UP TO DELETE
 420:	0169  60      	                LD      H,B         ;TXTUNF->UNFILLED AREA
 421:	016A  69      	                LD      L,C
 422:	016B  223608  	                LD      (TXTUNF),HL ;UPDATE
 423:	016E  C1      	ST4:            POP     BC          ;GET READY TO INSERT
 424:	016F  2A3608  	                LD      HL,(TXTUNF) ;BUT FIRST CHECK IF
 425:	0172  F1      	                POP     AF          ;THE LENGTH OF NEW LINE
 426:	0173  E5      	                PUSH    HL          ;IS 3 (LINE # AND CR)
 427:	0174  FE03    	                CP      3           ;THEN DO NOT INSERT
 428:	0176  28A6    	                JR      Z,WSTART    ;MUST CLEAR THE STACK
 429:	0178  85      	                ADD     A,L         ;COMPUTE NEW TXTUNF
 430:	0179  6F      	                LD      L,A
 431:	017A  3E00    	                LD      A,0
 432:	017C  8C      	                ADC     A,H
 433:	017D  67      	                LD      H,A         ;HL->NEW UNFILLED AREA
 434:	017E  11000F  	                LD      DE,TXTEND   ;CHECK TO SEE IF THERE
 435:	0181  E7      	                RST     RCOMP       ;COMP HL,DE - IS ENOUGH SPACE
 436:	0182  D28005  	                JP      NC,QSORRY   ;SORRY, NO ROOM FOR IT
 437:	0185  223608  	                LD      (TXTUNF),HL ;OK, UPDATE TXTUNF
 438:	0188  D1      	                POP     DE          ;DE->OLD UNFILLED AREA
 439:	0189  CDA206  	                CALL    MVDOWN
 440:	018C  D1      	                POP     DE          ;DE->BEGIN, HL->END
 441:	018D  E1      	                POP     HL
 442:	018E  CD9A06  	                CALL    MVUP        ;MOVE NEW LINE TO SAVE
 443:	0191  18A7    	                JR      ST3         ;AREA
 444:			
 445:			;*************************************************************
 446:			;
 447:			; WHAT FOLLOWS IS THE CODE TO EXECUTE DIRECT AND STATEMENT
 448:			; COMMANDS.  CONTROL IS TRANSFERED TO THESE POINTS VIA THE
 449:			; COMMAND TABLE LOOKUP CODE OF 'DIRECT' AND 'EXEC' IN LAST
 450:			; SECTION.  AFTER THE COMMAND IS EXECUTED, CONTROL IS
 451:			; TRANSFERED TO OTHERS SECTIONS AS FOLLOWS:
 452:			;
 453:			; FOR 'LIST', 'NEW', AND 'STOP': GO BACK TO 'WSTART'
 454:			; FOR 'RUN': GO EXECUTE THE FIRST STORED LINE IF ANY, ELSE
 455:			; GO BACK TO 'WSTART'.
 456:			; FOR 'GOTO' AND 'GOSUB': GO EXECUTE THE TARGET LINE.
 457:			; FOR 'RETURN' AND 'NEXT': GO BACK TO SAVED RETURN LINE.
 458:			; FOR ALL OTHERS: IF 'CURRENT' -> 0, GO TO 'WSTART', ELSE
 459:			; GO EXECUTE NEXT COMMAND.  (THIS IS DONE IN 'FINISH'.)
 460:			;*************************************************************
 461:			;
 462:			; *** NEW *** STOP *** RUN (& FRIENDS) *** & GOTO ***
 463:			;
 464:			; 'NEW(CR)' SETS 'TXTUNF' TO POINT TO 'TXTBGN'
 465:			;
 466:			; 'STOP(CR)' GOES BACK TO 'WSTART'
 467:			;
 468:			; 'RUN(CR)' FINDS THE FIRST STORED LINE, STORE ITS ADDRESS (IN
 469:			; 'CURRENT'), AND START EXECUTE IT.  NOTE THAT ONLY THOSE
 470:			; COMMANDS IN TAB2 ARE LEGAL FOR STORED PROGRAM.
 471:			;
 472:			; THERE ARE 3 MORE ENTRIES IN 'RUN':
 473:			; 'RUNNXL' FINDS NEXT LINE, STORES ITS ADDR. AND EXECUTES IT.
 474:			; 'RUNTSL' STORES THE ADDRESS OF THIS LINE AND EXECUTES IT.
 475:			; 'RUNSML' CONTINUES THE EXECUTION ON SAME LINE.
 476:			;
 477:			; 'GOTO EXPR(CR)' EVALUATES THE EXPRESSION, FIND THE TARGET
 478:			; LINE, AND JUMP TO 'RUNTSL' TO DO IT.
 479:			;
 480:	0193  CD4F05  	NEW:            CALL    ENDCHK  ;*** NEW(CR) ***
 481:	0196  210009  	                LD      HL,TXTBGN
 482:	0199  223608  	                LD      (TXTUNF),HL
 483:			;
 484:	019C  CD4F05  	STOP:           CALL    ENDCHK  ;*** STOP(CR) ***
 485:	019F  C31E01  	                JP      WSTART
 486:			
 487:	01A2  CD4F05  	RUN:            CALL    ENDCHK  ;*** RUN(CR) ***
 488:	01A5  110009  	                LD      DE,TXTBGN       ;FIRST SAVED LINE
 489:			;
 490:	01A8  210000  	RUNNXL:         LD      HL,0    ;*** RUNNXL ***
 491:	01AB  CDCD05  	                CALL    FNDLP   ;FIND WHATEVER LINE #
 492:	01AE  DA1E01  	                JP      C,WSTART ;C:PASSED TXTUNF, QUIT
 493:			;
 494:	01B1  EB      	RUNTSL:         EX      DE,HL   ;*** RUNTSL ***
 495:	01B2  223808  	                LD      (CURRNT),HL     ;SET 'CURRENT'->LINE #
 496:	01B5  EB      	                EX      DE,HL
 497:	01B6  13      	                INC     DE      ;BUMP PASS LINE #
 498:	01B7  13      	                INC     DE
 499:			;
 500:	01B8  CDFB00  	RUNSML:         CALL    CHKIO   ;*** RUNSML ***
 501:	01BB  213607  	                LD      HL,TAB2-1       ;FIND COMMAND IN TAB2
 502:	01BE  C3F506  	                JP      EXEC    ;AND EXECUTE IT
 503:			
 504:	01C1  DF      	GOTO:           RST     REXPR   ;*** GOTO EXPR ***
 505:	01C2  D5      	                PUSH    DE      ;SAVE FOR ERROR ROUTINE
 506:	01C3  CD4F05  	                CALL    ENDCHK  ;MUST FIND A CR
 507:	01C6  CDC505  	                CALL    FNDLN   ;FIND THE TARGET LINE
 508:	01C9  C2D100  	                JP      NZ,AHOW ;NO SUCH LINE #
 509:	01CC  F1      	                POP     AF      ;CLEAR THE PUSH DE
 510:	01CD  18E2    	                JR      RUNTSL  ;GO DO IT
 511:			;
 512:			;*************************************************************
 513:			;
 514:			; *** LIST *** & PRINT ***
 515:			;
 516:			; LIST HAS TWO FORMS:
 517:			; 'LIST(CR)' LISTS ALL SAVED LINES
 518:			; 'LIST #(CR)' START LIST AT THIS LINE #
 519:			; YOU CAN STOP THE LISTING BY CONTROL C KEY
 520:			;
 521:			; PRINT COMMAND IS 'PRINT ....;' OR 'PRINT ....(CR)'
 522:			; WHERE '....' IS A LIST OF EXPRESIONS, FORMATS, BACK-
 523:			; ARROWS, AND STRINGS.  THESE ITEMS ARE SEPERATED BY COMMAS.
 524:			;
 525:			; A FORMAT IS A POUND SIGN FOLLOWED BY A NUMBER.  IT CONTROLS
 526:			; THE NUMBER OF SPACES THE VALUE OF A EXPRESION IS GOING TO
 527:			; BE PRINTED.  IT STAYS EFFECTIVE FOR THE REST OF THE PRINT
 528:			; COMMAND UNLESS CHANGED BY ANOTHER FORMAT.  IF NO FORMAT IS
 529:			; SPECIFIED, 8 POSITIONS WILL BE USED.
 530:			;
 531:			; NUMBER BASE IS SET BY PERCENT SIGN FOLLOEWED BY A NUMBER
 532:			; BETWEEN 2 and 16. VALUES ARE PRINTED AS UNSIGNED TO THIS BASE
 533:			; FOR THE REST OF THIS PRINT COMMAND UNLESS CHANGED BY
 534:			; ANOTHER BASE. IF NO BASE IS PROVIDED NUMBERS ARE SIGNED DECIMAL.
 535:			;
 536:			; A STRING IS QUOTED IN A PAIR OF SINGLE QUOTES OR A PAIR OF
 537:			; DOUBLE QUOTES.
 538:			;
 539:			; A BACK-ARROW (UNDERLINE) ALONE MEANS GENERATE A (CR) WITHOUT (LF).
 540:			;
 541:			; A (CRLF) IS GENERATED AFTER THE ENTIRE LIST HAS BEEN
 542:			; PRINTED OR IF THE LIST IS A NULL LIST.  HOWEVER IF THE LIST
 543:			; ENDED WITH A COMMA, NO (CRLF) IS GENERATED.
 544:			;
 545:	01CF  CD7E00  	LIST_:          CALL    TSTNUM  ;TEST IF THERE IS A #
 546:	01D2  CD4F05  	                CALL    ENDCHK  ;IF NO # WE GET A 0
 547:	01D5  CDC505  	                CALL    FNDLN   ;FIND THIS OR NEXT LINE
 548:	01D8  DA1E01  	LS1:            JP      C,WSTART ;C:PASSED TXTUNF
 549:	01DB  CD8306  	                CALL    PRTLN   ;PRINT THE LINE
 550:	01DE  CDFB00  	                CALL    CHKIO   ;STOP IF HIT CONTROL-C
 551:	01E1  CDCD05  	                CALL    FNDLP   ;FIND NEXT LINE
 552:	01E4  18F2    	                JR      LS1     ;AND LOOP BACK
 553:			
 554:	01E6  0E08    	PRINT:          LD      C,8     ;C = # OF SPACES
 555:	01E8  AF      	                XOR     A       ;DEFAULT BASE FOR PRTNUM
 556:	01E9  323508  	                LD      (PNBASE),A
 557:	01EC  CF3B06  	                TSTC     ';',PR2 ;IF NULL LIST & ";"
 558:	01EF  CD0E00  	                CALL    CRLF    ;GIVE CR-LF AND
 559:	01F2  C3B801  	                JP      RUNSML  ;CONTINUE SAME LINE
 560:	01F5  CF0D06  	PR2:            TSTC     CR,PR0  ;IF NULL LIST (CR)
 561:	01F8  CD0E00  	                CALL    CRLF    ;ALSO GIVE CR-LF AND
 562:	01FB  C3A801  	                JP      RUNNXL  ;GO TO NEXT LINE
 563:	01FE  CF2304  	PR0:            TSTC     '#',PR5 ;ELSE IS IT FORMAT?
 564:	0201  DF      	                RST     REXPR   ;YES, EVALUATE EXPR.
 565:	0202  4D      	                LD      C,L     ;AND SAVE IT IN C
 566:	0203  181A    	                JR      PR3     ;LOOK FOR MORE TO PRINT
 567:	0205  CF2511  	PR5:            TSTC     '%',PR1 ;ELSE IS IT PRTNUM BASE?
 568:	0208  DF      	                RST     REXPR   ;YES, EVALUATE EXPR.
 569:	0209  7D      	                LD      A,L     ;GET THE LOW PART
 570:	020A  FE01    	                CP      1       ;EITHER 0 OR >= 2?
 571:	020C  CAD000  	                JP      Z,QHOW  ;ERROR
 572:	020F  FE11    	                CP      17      ;BASE > 16?
 573:	0211  D2D000  	                JP      NC,QHOW ;ERROR
 574:	0214  323508  	                LD      (PNBASE),A      ;IN PNBASE
 575:	0217  1806    	                JR      PR3     ;LOOK FOR MORE TO PRINT
 576:	0219  CDF505  	PR1:            CALL    QTSTG   ;OR IS IT A STRING?
 577:	021C  C32F02  	                JP      PR8     ;HACK JP!! IF NOT, MUST BE EXPR.
 578:	021F  CF2C05  	PR3:            TSTC     $2C,PR6 ;IF ",", GO FIND NEXT
 579:	0222  CD4005  	                CALL    FIN     ;IN THE LIST.
 580:	0225  18D7    	                JR      PR0     ;LIST CONTINUES
 581:	0227  AF      	PR6:            XOR     A       ;END OF LIST REACHED
 582:	0228  323508  	                LD      (PNBASE),A      ;RESET DEFAULT BASE
 583:	022B  CD0E00  	                CALL    CRLF    ;LIST ENDS WITH CRLF
 584:	022E  F7      	                RST     RFINISH ;FINISH
 585:	022F  DF      	PR8:            RST     REXPR   ;EVALUATE THE EXPR
 586:	0230  C5      	                PUSH    BC
 587:	0231  CD2306  	                CALL    PRTNUM  ;PRINT THE VALUE
 588:	0234  C1      	                POP     BC
 589:	0235  18E8    	                JR      PR3     ;MORE TO PRINT?
 590:			;
 591:			;*************************************************************
 592:			;
 593:			; *** GOSUB *** & RETURN ***
 594:			;
 595:			; 'GOSUB EXPR;' OR 'GOSUB EXPR (CR)' IS LIKE THE 'GOTO'
 596:			; COMMAND, EXCEPT THAT THE CURRENT TEXT POINTER, STACK POINTER
 597:			; ETC. ARE SAVE SO THAT EXECUTION CAN BE CONTINUED AFTER THE
 598:			; SUBROUTINE 'RETURN'.  IN ORDER THAT 'GOSUB' CAN BE NESTED
 599:			; (AND EVEN RECURSIVE), THE SAVE AREA MUST BE STACKED.
 600:			; THE STACK POINTER IS SAVED IN 'STKGOS', THE OLD 'STKGOS' IS
 601:			; SAVED IN THE STACK.  IF WE ARE IN THE MAIN ROUTINE, 'STKGOS'
 602:			; IS ZERO (THIS WAS DONE BY THE "MAIN" SECTION OF THE CODE),
 603:			; BUT WE STILL SAVE IT AS A FLAG FOR NO FURTHER 'RETURN'S.
 604:			;
 605:			; 'RETURN(CR)' UNDOS EVERYTHING THAT 'GOSUB' DID, AND THUS
 606:			; RETURN THE EXECUTION TO THE COMMAND AFTER THE MOST RECENT
 607:			; 'GOSUB'.  IF 'STKGOS' IS ZERO, IT INDICATES THAT WE
 608:			; NEVER HAD A 'GOSUB' AND IS THUS AN ERROR.
 609:			;
 610:	0237  CDCA06  	GOSUB:          CALL    PUSHA   ;SAVE THE CURRENT "FOR"
 611:	023A  DF      	                RST     REXPR   ;PARAMETERS
 612:	023B  D5      	                PUSH    DE      ;AND TEXT POINTER
 613:	023C  CDC505  	                CALL    FNDLN   ;FIND THE TARGET LINE
 614:	023F  C2D100  	                JP      NZ,AHOW ;NOT THERE. SAY "HOW?"
 615:	0242  2A3808  	                LD      HL,(CURRNT)     ;FOUND IT, SAVE OLD
 616:	0245  E5      	                PUSH    HL      ;'CURRNT' OLD 'STKGOS'
 617:	0246  2A3A08  	                LD      HL,(STKGOS)
 618:	0249  E5      	                PUSH    HL
 619:	024A  210000  	                LD      HL,0    ;AND LOAD NEW ONES
 620:	024D  224008  	                LD      (LOPVAR),HL
 621:	0250  39      	                ADD     HL,SP
 622:	0251  223A08  	                LD      (STKGOS),HL
 623:	0254  C3B101  	                JP      RUNTSL  ;THEN RUN THAT LINE
 624:			;
 625:	0257  CD4F05  	RETURN:         CALL    ENDCHK  ;THERE MUST BE A CR
 626:	025A  2A3A08  	                LD      HL,(STKGOS)     ;OLD STACK POINTER
 627:	025D  7C      	                LD      A,H     ;0 MEANS NOT EXIST
 628:	025E  B5      	                OR      L
 629:	025F  CA5305  	                JP      Z,QWHAT ;SO, WE SAY: "WHAT?"
 630:	0262  F9      	                LD      SP,HL   ;ELSE, RESTORE IT
 631:	0263  E1      	                POP     HL
 632:	0264  223A08  	                LD      (STKGOS),HL     ;AND THE OLD 'STKGOS'
 633:	0267  E1      	                POP     HL
 634:	0268  223808  	                LD      (CURRNT),HL     ;AND THE OLD 'CURRNT'
 635:	026B  D1      	                POP     DE      ;OLD TEXT POINTER
 636:	026C  CDAF06  	                CALL    POPA    ;OLD "FOR" PARAMETERS
 637:	026F  F7      	                RST     RFINISH ;AND WE ARE BACK HOME
 638:			;
 639:			;*************************************************************
 640:			;
 641:			; *** FOR *** & NEXT ***
 642:			;
 643:			; 'FOR' HAS TWO FORMS:
 644:			; 'FOR VAR=EXP1 TO EXP2 STEP EXP3' AND 'FOR VAR=EXP1 TO EXP2'
 645:			; THE SECOND FORM MEANS THE SAME THING AS THE FIRST FORM WITH
 646:			; EXP3=1.  (I.E., WITH A STEP OF +1.)
 647:			; TBI WILL FIND THE VARIABLE VAR, AND SET ITS VALUE TO THE
 648:			; CURRENT VALUE OF EXP1.  IT ALSO EVALUATES EXP2 AND EXP3
 649:			; AND SAVE ALL THESE TOGETHER WITH THE TEXT POINTER ETC. IN
 650:			; THE 'FOR' SAVE AREA, WHICH CONSISTS OF 'LOPVAR', 'LOPINC',
 651:			; 'LOPLMT', 'LOPLN', AND 'LOPPT'.  IF THERE IS ALREADY SOME-
 652:			; THING IN THE SAVE AREA (THIS IS INDICATED BY A NON-ZERO
 653:			; 'LOPVAR'), THEN THE OLD SAVE AREA IS SAVED IN THE STACK
 654:			; BEFORE THE NEW ONE OVERWRITES IT.
 655:			; TBI WILL THEN DIG IN THE STACK AND FIND OUT IF THIS SAME
 656:			; VARIABLE WAS USED IN ANOTHER CURRENTLY ACTIVE 'FOR' LOOP.
 657:			; IF THAT IS THE CASE, THEN THE OLD 'FOR' LOOP IS DEACTIVATED.
 658:			; (PURGED FROM THE STACK..)
 659:			;
 660:			; 'NEXT VAR' SERVES AS THE LOGICAL (NOT NECESSARILLY PHYSICAL)
 661:			; END OF THE 'FOR' LOOP.  THE CONTROL VARIABLE VAR. IS CHECKED
 662:			; WITH THE 'LOPVAR'.  IF THEY ARE NOT THE SAME, TBI DIGS IN
 663:			; THE STACK TO FIND THE RIGHT ONE AND PURGES ALL THOSE THAT
 664:			; DID NOT MATCH.  EITHER WAY, TBI THEN ADDS THE 'STEP' TO
 665:			; THAT VARIABLE AND CHECK THE RESULT WITH THE LIMIT.  IF IT
 666:			; IS WITHIN THE LIMIT, CONTROL LOOPS BACK TO THE COMMAND
 667:			; FOLLOWING THE 'FOR'.  IF OUTSIDE THE LIMIT, THE SAVE AREA
 668:			; IS PURGED AND EXECUTION CONTINUES.
 669:			;
 670:	0270  CDCA06  	FOR:            CALL    PUSHA           ;SAVE THE OLD SAVE AREA
 671:	0273  CD3105  	                CALL    SETVAL          ;SET THE CONTROL VAR.
 672:	0276  2B      	                DEC     HL              ;HL IS ITS ADDRESS
 673:	0277  224008  	                LD      (LOPVAR),HL     ;SAVE THAT
 674:	027A  21B307  	                LD      HL,TAB5-1       ;USE 'EXEC' TO LOOK
 675:	027D  C3F506  	                JP      EXEC            ;FOR THE WORD 'TO'
 676:	0280  DF      	FR1:            RST     REXPR           ;EVALUATE THE LIMIT
 677:	0281  224408  	                LD      (LOPLMT),HL     ;SAVE THAT
 678:	0284  21B907  	                LD      HL,TAB6-1       ;USE 'EXEC' TO LOOK
 679:	0287  C3F506  	                JP      EXEC            ;FOR THE WORD 'STEP'
 680:	028A  DF      	FR2:            RST     REXPR           ;FOUND IT, GET STEP
 681:	028B  1803    	                JR      FR4
 682:	028D  210100  	FR3:            LD      HL,1H           ;NOT FOUND, SET TO 1
 683:	0290  224208  	FR4:            LD      (LOPINC),HL     ;SAVE THAT TOO
 684:	0293  2A3808  	FR5:            LD      HL,(CURRNT)     ;SAVE CURRENT LINE #
 685:	0296  224608  	                LD      (LOPLN),HL
 686:	0299  EB      	                EX      DE,HL           ;AND TEXT POINTER
 687:	029A  224808  	                LD      (LOPPT),HL
 688:	029D  010A00  	                LD      BC,0AH          ;DIG INTO STACK TO
 689:	02A0  2A4008  	                LD      HL,(LOPVAR)     ;FIND 'LOPVAR'
 690:	02A3  EB      	                EX      DE,HL
 691:	02A4  60      	                LD      H,B
 692:	02A5  68      	                LD      L,B             ;HL=0 NOW
 693:	02A6  39      	                ADD     HL,SP           ;HERE IS THE STACK
 694:	02A7  3E      	                .DB     3EH             ;HACK SKIP "ADD HL,BC"
 695:	02A8  09      	FR7:            ADD     HL,BC           ;EACH LEVEL IS 10 DEEP
 696:	02A9  7E      	                LD      A,(HL)          ;GET THAT OLD 'LOPVAR'
 697:	02AA  23      	                INC     HL
 698:	02AB  B6      	                OR      (HL)
 699:	02AC  2818    	                JR      Z,FR8           ;0 SAYS NO MORE IN IT
 700:	02AE  7E      	                LD      A,(HL)
 701:	02AF  2B      	                DEC     HL
 702:	02B0  BA      	                CP      D               ;SAME AS THIS ONE?
 703:	02B1  20F5    	                JR      NZ,FR7
 704:	02B3  7E      	                LD      A,(HL)          ;THE OTHER HALF?
 705:	02B4  BB      	                CP      E
 706:	02B5  20F1    	                JR      NZ,FR7
 707:	02B7  EB      	                EX      DE,HL           ;YES, FOUND ONE
 708:	02B8  210000  	                LD      HL,0H
 709:	02BB  39      	                ADD     HL,SP           ;TRY TO MOVE SP
 710:	02BC  44      	                LD      B,H
 711:	02BD  4D      	                LD      C,L
 712:	02BE  210A00  	                LD      HL,0AH
 713:	02C1  19      	                ADD     HL,DE
 714:	02C2  CDA206  	                CALL    MVDOWN          ;AND PURGE 10 WORDS
 715:	02C5  F9      	                LD      SP,HL           ;IN THE STACK
 716:	02C6  2A4808  	FR8:            LD      HL,(LOPPT)      ;JOB DONE, RESTORE DE
 717:	02C9  EB      	                EX      DE,HL
 718:	02CA  F7      	                RST     RFINISH         ;AND CONTINUE
 719:			;
 720:	02CB  FF      	NEXT:           RST     RTSTV           ;GET ADDRESS OF VAR.
 721:	02CC  DA5305  	                JP      C,QWHAT         ;NO VARIABLE, "WHAT?"
 722:	02CF  223C08  	                LD      (VARNXT),HL     ;YES, SAVE IT
 723:	02D2  D5      	NX0:            PUSH    DE              ;SAVE TEXT POINTER
 724:	02D3  EB      	                EX      DE,HL
 725:	02D4  2A4008  	                LD      HL,(LOPVAR)     ;GET VAR. IN 'FOR'
 726:	02D7  7C      	                LD      A,H
 727:	02D8  B5      	                OR      L               ;0 SAYS NEVER HAD ONE
 728:	02D9  CA5405  	                JP      Z,AWHAT         ;SO WE ASK: "WHAT?"
 729:	02DC  E7      	                RST     RCOMP           ;ELSE WE CHECK THEM
 730:	02DD  2809    	                JR      Z,NX3           ;OK, THEY AGREE
 731:	02DF  D1      	                POP     DE              ;NO, LET'S SEE
 732:	02E0  CDAF06  	                CALL    POPA            ;PURGE CURRENT LOOP
 733:	02E3  2A3C08  	                LD      HL,(VARNXT)     ;AND POP ONE LEVEL
 734:	02E6  18EA    	                JR      NX0             ;GO CHECK AGAIN
 735:	02E8  5E      	NX3:            LD      E,(HL)          ;COME HERE WHEN AGREED
 736:	02E9  23      	                INC     HL
 737:	02EA  56      	                LD      D,(HL)          ;DE=VALUE OF VAR.
 738:	02EB  2A4208  	                LD      HL,(LOPINC)
 739:	02EE  E5      	                PUSH    HL
 740:	02EF  7C      	                LD      A,H
 741:	02F0  AA      	                XOR     D
 742:	02F1  7A      	                LD      A,D
 743:	02F2  19      	                ADD     HL,DE           ;ADD ONE STEP
 744:	02F3  FAFA02  	                JP      M,NX4
 745:	02F6  AC      	                XOR     H
 746:	02F7  FA1B03  	                JP      M,NX5
 747:	02FA  EB      	NX4:            EX      DE,HL
 748:	02FB  2A4008  	                LD      HL,(LOPVAR)     ;PUT IT BACK
 749:	02FE  73      	                LD      (HL),E
 750:	02FF  23      	                INC     HL
 751:	0300  72      	                LD      (HL),D
 752:	0301  2A4408  	                LD      HL,(LOPLMT)     ;HL->LIMIT
 753:	0304  F1      	                POP     AF              ;OLD HL
 754:	0305  B7      	                OR      A
 755:	0306  F20A03  	                JP      P,NX1           ;STEP > 0
 756:	0309  EB      	                EX      DE,HL           ;STEP < 0
 757:	030A  CD2905  	NX1:            CALL    CKHLDE          ;COMPARE WITH LIMIT
 758:	030D  D1      	                POP     DE              ;RESTORE TEXT POINTER
 759:	030E  380D    	                JR      C,NX2           ;OUTSIDE LIMIT
 760:	0310  2A4608  	                LD      HL,(LOPLN)      ;WITHIN LIMIT, GO
 761:	0313  223808  	                LD      (CURRNT),HL     ;BACK TO THE SAVED
 762:	0316  2A4808  	                LD      HL,(LOPPT)      ;'CURRNT' AND TEXT
 763:	0319  EB      	                EX      DE,HL           ;POINTER
 764:	031A  F7      	                RST     RFINISH
 765:	031B  E1      	NX5:            POP     HL
 766:	031C  D1      	                POP     DE
 767:	031D  CDAF06  	NX2:            CALL    POPA            ;PURGE THIS LOOP
 768:	0320  F7      	                RST     RFINISH
 769:			;
 770:			;*************************************************************
 771:			;
 772:			; *** REM *** IF *** INPUT *** & LET (& DEFLT) ***
 773:			;
 774:			; 'REM' CAN BE FOLLOWED BY ANYTHING AND IS IGNORED BY TBI.
 775:			; TBI TREATS IT LIKE AN 'IF' WITH A FALSE CONDITION.
 776:			;
 777:			; 'IF' IS FOLLOWED BY AN EXPR. AS A CONDITION AND ONE OR MORE
 778:			; COMMANDS (INCLUDING OTHER 'IF'S) SEPERATED BY SEMI-COLONS.
 779:			; NOTE THAT THE WORD 'THEN' IS NOT USED.  TBI EVALUATES THE
 780:			; EXPR. IF IT IS NON-ZERO, EXECUTION CONTINUES.  IF THE
 781:			; EXPR. IS ZERO, THE COMMANDS THAT FOLLOWS ARE IGNORED AND
 782:			; EXECUTION CONTINUES AT THE NEXT LINE.
 783:			;
 784:			; 'INPUT' COMMAND IS LIKE THE 'PRINT' COMMAND, AND IS FOLLOWED
 785:			; BY A LIST OF ITEMS.  IF THE ITEM IS A STRING IN SINGLE OR
 786:			; DOUBLE QUOTES, OR IS A BACK-ARROW, IT HAS THE SAME EFFECT AS
 787:			; IN 'PRINT'.  IF AN ITEM IS A VARIABLE, THIS VARIABLE NAME IS
 788:			; PRINTED OUT FOLLOWED BY A COLON.  THEN TBI WAITS FOR AN
 789:			; EXPR. TO BE TYPED IN.  THE VARIABLE IS THEN SET TO THE
 790:			; VALUE OF THIS EXPR.  IF THE VARIABLE IS PROCEDED BY A STRING
 791:			; (AGAIN IN SINGLE OR DOUBLE QUOTES), THE STRING WILL BE
 792:			; PRINTED FOLLOWED BY A COLON.  TBI THEN WAITS FOR INPUT EXPR.
 793:			; AND SET THE VARIABLE TO THE VALUE OF THE EXPR.
 794:			;
 795:			; IF THE INPUT EXPR. IS INVALID, TBI WILL PRINT "WHAT?",
 796:			; "HOW?" OR "SORRY" AND REPRINT THE PROMPT AND REDO THE INPUT.
 797:			; THE EXECUTION WILL NOT TERMINATE UNLESS YOU TYPE CONTROL-C.
 798:			; THIS IS HANDLED IN 'INPERR'.
 799:			;
 800:			; 'LET' IS FOLLOWED BY A LIST OF ITEMS SEPERATED BY COMMAS.
 801:			; EACH ITEM CONSISTS OF A VARIABLE, AN EQUAL SIGN, AND AN EXPR.
 802:			; TBI EVALUATES THE EXPR. AND SET THE VARIABLE TO THAT VALUE.
 803:			; TBI WILL ALSO HANDLE 'LET' COMMAND WITHOUT THE WORD 'LET'.
 804:			; THIS IS DONE BY 'DEFLT'.
 805:			;
 806:	0321  210000  	REM:            LD      HL,0H   ;*** REM ***
 807:	0324  3E      	                .DB     3EH             ;SKIP RST, THIS IS LIKE 'IF 0'
 808:			;
 809:	0325  DF      	IF_:            RST     REXPR   ;*** IF ***
 810:	0326  7C      	                LD      A,H     ;IS THE EXPR.=0?
 811:	0327  B5      	                OR      L
 812:	0328  C2B801  	                JP      NZ,RUNSML       ;NO, CONTINUE
 813:	032B  CDE205  	                CALL    FNDSKP  ;YES, SKIP REST OF LINE
 814:	032E  D2B101  	                JP      NC,RUNTSL       ;AND RUN THE NEXT LINE
 815:	0331  C31E01  	                JP      WSTART  ;IF NO NEXT, RE-START
 816:			;
 817:	0334  2A3E08  	INPERR:         LD      HL,(STKINP)     ;*** INPERR ***
 818:	0337  F9      	                LD      SP,HL   ;RESTORE OLD SP
 819:	0338  E1      	                POP     HL      ;AND OLD 'CURRNT'
 820:	0339  223808  	                LD      (CURRNT),HL
 821:	033C  D1      	                POP     DE      ;AND OLD TEXT POINTER
 822:	033D  D1      	                POP     DE      ;REDO INPUT
 823:			;
 824:	033E          	INPUT:          ;*** INPUT ***
 825:	033E  D5      	IP1:            PUSH    DE      ;SAVE IN CASE OF ERROR
 826:	033F  CDF505  	                CALL    QTSTG   ;IS NEXT ITEM A STRING?
 827:	0342  C34A03  	                JP      IP2     ;HACK JP!! NO
 828:	0345  FF      	                RST     RTSTV   ;YES, BUT FOLLOWED BY A
 829:	0346  3839    	                JR      C,IP4   ;VARIABLE?   NO.
 830:	0348  1810    	                JR      IP3     ;YES.  INPUT VARIABLE
 831:	034A  D5      	IP2:            PUSH    DE      ;SAVE FOR 'PRTSTG'
 832:	034B  FF      	                RST     RTSTV   ;MUST BE VARIABLE NOW
 833:	034C  DA5305  	                JP      C,QWHAT ;"WHAT?" IT IS NOT?
 834:	034F  1A      	                LD      A,(DE)  ;GET READY FOR 'PRTSTG'
 835:	0350  4F      	                LD      C,A
 836:	0351  97      	                SUB     A
 837:	0352  12      	                LD      (DE),A
 838:	0353  D1      	                POP     DE
 839:	0354  CDEA05  	                CALL    PRTSTG  ;PRINT STRING AS PROMPT
 840:	0357  79      	                LD      A,C     ;RESTORE TEXT
 841:	0358  1B      	                DEC     DE
 842:	0359  12      	                LD      (DE),A
 843:	035A  D5      	IP3:            PUSH    DE      ;SAVE TEXT POINTER
 844:	035B  EB      	                EX      DE,HL
 845:	035C  2A3808  	                LD      HL,(CURRNT)     ;ALSO SAVE 'CURRNT'
 846:	035F  E5      	                PUSH    HL
 847:	0360  213E03  	                LD      HL,IP1  ;A NEGATIVE NUMBER
 848:	0363  223808  	                LD      (CURRNT),HL     ;AS A FLAG
 849:	0366  210000  	                LD      HL,0H   ;SAVE SP TOO
 850:	0369  39      	                ADD     HL,SP
 851:	036A  223E08  	                LD      (STKINP),HL
 852:	036D  D5      	                PUSH    DE      ;OLD HL
 853:	036E  3E3A    	                LD      A,':'   ;PRINT THIS TOO
 854:	0370  CD8605  	                CALL    GETLN   ;AND GET A LINE
 855:	0373  11800F  	                LD      DE,BUFFER       ;POINTS TO BUFFER
 856:	0376  DF      	                RST     REXPR   ;EVALUATE INPUT
 857:			                ;NOP            ;??? CAN BE 'CALL ENDCHK'
 858:			                ;NOP
 859:			                ;NOP
 860:	0377  D1      	                POP     DE      ;OK, GET OLD HL
 861:	0378  EB      	                EX      DE,HL
 862:	0379  73      	                LD      (HL),E  ;SAVE VALUE IN VAR.
 863:	037A  23      	                INC     HL
 864:	037B  72      	                LD      (HL),D
 865:	037C  E1      	                POP     HL      ;GET OLD 'CURRNT'
 866:	037D  223808  	                LD      (CURRNT),HL
 867:	0380  D1      	                POP     DE      ;AND OLD TEXT POINTER
 868:	0381  F1      	IP4:            POP     AF      ;PURGE JUNK IN STACK
 869:	0382  CF2C02  	                TSTC     $2C,IP5 ;IS NEXT CH. ','?
 870:	0385  18B7    	                JR      IP1     ;YES, MORE ITEMS.
 871:	0387  F7      	IP5:            RST     RFINISH
 872:			;
 873:	0388  1A      	DEFLT:          LD      A,(DE)  ;***  DEFLT ***
 874:	0389  FE0D    	                CP      CR      ;EMPTY LINE IS OK
 875:	038B  2808    	                JR      Z,LT1   ;ELSE IT IS 'LET'
 876:			;
 877:	038D  CD3105  	LET:            CALL    SETVAL  ;*** LET ***
 878:	0390  CF2C02  	                TSTC    $2C,LT1  ;SET VALUE TO VAR.
 879:	0393  18F8    	                JR      LET     ;ITEM BY ITEM
 880:	0395  F7      	LT1:            RST     RFINISH ;UNTIL FINISH
 881:			;
 882:			;*************************************************************
 883:			;
 884:			; *** EXPR ***
 885:			;
 886:			; 'EXPR' EVALUATES ARITHMETICAL OR LOGICAL EXPRESSIONS.
 887:			; <EXPR>::<EXPR2>
 888:			;         <EXPR2><REL.OP.><EXPR2>
 889:			; WHERE <REL.OP.> IS ONE OF THE OPERATORS IN TAB8 AND THE
 890:			; RESULT OF THESE OPERATIONS IS 1 IF TRUE AND 0 IF FALSE.
 891:			; <EXPR2>::=(+ OR -)<EXPR3>(+ OR -<EXPR3>)(....)
 892:			; WHERE () ARE OPTIONAL AND (....) ARE OPTIONAL REPEATS.
 893:			; <EXPR3>::=<EXPR4>(* OR /><EXPR4>)(....)
 894:			; <EXPR4>::=<VARIABLE>
 895:			;           <FUNCTION>
 896:			;           (<EXPR>)
 897:			; <EXPR> IS RECURSIVE SO THAT VARIABLE '@' CAN HAVE AN <EXPR>
 898:			; AS INDEX, FUNCTIONS CAN HAVE AN <EXPR> AS ARGUMENTS, AND
 899:			; <EXPR4> CAN BE AN <EXPR> IN PARANTHESE.
 900:			;
 901:			;EXPR:          CALL    EXPR2   ;THIS IS AT LOC. 18
 902:			;               PUSH    HL      ;SAVE <EXPR2> VALUE
 903:			;               JP      EXPR1   ;REST OF IT AT EXPR1
 904:	0396  21C107  	EXPR1:          LD      HL,TAB8-1       ;LOOKUP REL.OP.
 905:	0399  C3F506  	                JP      EXEC    ;GO DO IT
 906:			;
 907:	039C  CDC503  	XP11:           CALL    XP18    ;REL.OP.">="
 908:	039F  D8      	                RET     C       ;NO, RETURN HL=0
 909:	03A0  6F      	                LD      L,A     ;YES, RETURN HL=1
 910:	03A1  C9      	                RET
 911:			;
 912:	03A2  CDC503  	XP12:           CALL    XP18    ;REL.OP."#" OR "!="
 913:	03A5  C8      	                RET     Z       ;FALSE, RETURN HL=0
 914:	03A6  6F      	                LD      L,A     ;TRUE, RETURN HL=1
 915:	03A7  C9      	                RET
 916:			;
 917:	03A8  CDC503  	XP13:           CALL    XP18    ;REL.OP.">"
 918:	03AB  C8      	                RET     Z       ;FALSE
 919:	03AC  D8      	                RET     C       ;ALSO FALSE, HL=0
 920:	03AD  6F      	                LD      L,A     ;TRUE, HL=1
 921:	03AE  C9      	                RET
 922:			;
 923:	03AF  CDC503  	XP14:           CALL    XP18    ;REL.OP."<="
 924:	03B2  6F      	                LD      L,A     ;SET HL=1
 925:	03B3  C8      	                RET     Z       ;REL. TRUE, RETURN
 926:	03B4  D8      	                RET     C
 927:	03B5  6C      	                LD      L,H     ;ELSE SET HL=0
 928:	03B6  C9      	                RET
 929:			;
 930:	03B7  CDC503  	XP15:           CALL    XP18    ;REL.OP."=" OR "=="
 931:	03BA  C0      	                RET     NZ      ;FALSE, RETURN HL=0
 932:	03BB  6F      	                LD      L,A     ;ELSE SET HL=1
 933:	03BC  C9      	                RET
 934:			;
 935:	03BD  CDC503  	XP16:           CALL    XP18    ;REL.OP."<"
 936:	03C0  D0      	                RET     NC      ;FALSE, RETURN HL=0
 937:	03C1  6F      	                LD      L,A     ;ELSE SET HL=1
 938:	03C2  C9      	                RET
 939:			;
 940:	03C3  E1      	XP17:           POP     HL      ;NOT .REL.OP
 941:	03C4  C9      	                RET             ;RETURN HL=<EXPR2>
 942:			;
 943:	03C5  79      	XP18:           LD      A,C     ;SUBROUTINE FOR ALL
 944:	03C6  E1      	                POP     HL      ;REL.OP.'S
 945:	03C7  C1      	                POP     BC
 946:	03C8  E5      	                PUSH    HL      ;REVERSE TOP OF STACK
 947:	03C9  C5      	                PUSH    BC
 948:	03CA  4F      	                LD      C,A
 949:	03CB  CDDA03  	                CALL    EXPR2   ;GET 2ND <EXPR2>
 950:	03CE  EB      	                EX      DE,HL   ;VALUE IN DE NOW
 951:	03CF  E3      	                EX      (SP),HL ;1ST <EXPR2> IN HL
 952:	03D0  CD2905  	                CALL    CKHLDE  ;COMPARE 1ST WITH 2ND
 953:	03D3  D1      	                POP     DE      ;RESTORE TEXT POINTER
 954:	03D4  210000  	                LD      HL,0H   ;SET HL=0, A=1
 955:	03D7  3E01    	                LD      A,1
 956:	03D9  C9      	                RET
 957:			;
 958:	03DA  CF2D05  	EXPR2:          TSTC    '-',XP21 ;NEGATIVE SIGN?
 959:	03DD  210000  	                LD      HL,0H   ;YES, FAKE '0-'
 960:	03E0  1821    	                JR      XP26    ;TREAT LIKE SUBTRACT
 961:			;
 962:	03E2  CF2B00  	XP21:           TSTC    '+',XP22 ;POSITIVE SIGN? IGNORE
 963:	03E5  CD0C04  	XP22:           CALL    EXPR3   ;1ST <EXPR3>
 964:	03E8  CF2B15  	XP23:           TSTC    '+',XP25 ;ADD?
 965:	03EB  E5      	                PUSH    HL      ;YES, SAVE VALUE
 966:	03EC  CD0C04  	                CALL    EXPR3   ;GET 2ND <EXPR3>
 967:	03EF  EB      	XP24:           EX      DE,HL   ;2ND IN DE
 968:	03F0  E3      	                EX      (SP),HL ;1ST IN HL
 969:	03F1  7C      	                LD      A,H     ;COMPARE SIGN
 970:	03F2  AA      	                XOR     D
 971:	03F3  7A      	                LD      A,D
 972:	03F4  19      	                ADD     HL,DE
 973:	03F5  D1      	                POP     DE      ;RESTORE TEXT POINTER
 974:	03F6  FAE803  	                JP      M,XP23  ;1ST AND 2ND SIGN DIFFER
 975:	03F9  AC      	                XOR     H       ;1ST AND 2ND SIGN EQUAL
 976:	03FA  F2E803  	                JP      P,XP23  ;SO IS RESULT
 977:			;
 978:	03FD  C3D000  	                JP      QHOW    ;ELSE WE HAVE OVERFLOW
 979:			;
 980:	0400  CF2D7F  	XP25:           TSTC    '-',XP42 ;SUBTRACT?
 981:	0403  E5      	XP26:           PUSH    HL      ;YES, SAVE 1ST <EXPR3>
 982:	0404  CD0C04  	                CALL    EXPR3   ;GET 2ND <EXPR3>
 983:	0407  CD1405  	                CALL    CHGSGN  ;NEGATE
 984:	040A  18E3    	                JR      XP24    ;AND ADD THEM
 985:			;
 986:	040C  CD6704  	EXPR3:          CALL    EXPR4   ;GET 1ST <EXPR4>
 987:	040F  CF2A29  	XP31:           TSTC    '*',XP34 ;MULTIPLY?
 988:	0412  E5      	                PUSH    HL      ;YES, SAVE 1ST
 989:	0413  CD6704  	                CALL    EXPR4   ;AND GET 2ND <EXPR4>
 990:	0416  0600    	                LD      B,0H    ;CLEAR B FOR SIGN
 991:	0418  CD1105  	                CALL    CHKSGN  ;CHECK SIGN
 992:	041B  E3      	                EX      (SP),HL ;1ST IN HL
 993:	041C  CD1105  	                CALL    CHKSGN  ;CHECK SIGN OF 1ST
 994:	041F  EB      	                EX      DE,HL
 995:	0420  E3      	                EX      (SP),HL
 996:	0421  7C      	                LD      A,H     ;IS HL > 255 ?
 997:	0422  B7      	                OR      A
 998:	0423  2806    	                JR      Z,XP32  ;NO
 999:	0425  7A      	                LD      A,D     ;YES, HOW ABOUT DE
1000:	0426  B2      	                OR      D
1001:	0427  EB      	                EX      DE,HL   ;PUT SMALLER IN HL
1002:	0428  C2D100  	                JP      NZ,AHOW ;ALSO >, WILL OVERFLOW
1003:	042B  7D      	XP32:           LD      A,L     ;THIS IS DUMB
1004:	042C  210000  	                LD      HL,0H   ;CLEAR RESULT
1005:	042F  B7      	                OR      A       ;ADD AND COUNT
1006:	0430  2828    	                JR      Z,XP35
1007:	0432  19      	XP33:           ADD     HL,DE
1008:	0433  DAD100  	                JP      C,AHOW  ;OVERFLOW
1009:	0436  3D      	                DEC     A
1010:	0437  20F9    	                JR      NZ,XP33
1011:	0439  181F    	                JR      XP35    ;FINISHED
1012:			;
1013:	043B  CF2F44  	XP34:           TSTC    '/',XP42 ;DIVIDE?
1014:	043E  E5      	                PUSH    HL      ;YES, SAVE 1ST <EXPR4>
1015:	043F  CD6704  	                CALL    EXPR4   ;AND GET THE SECOND ONE
1016:	0442  0600    	                LD      B,0H    ;CLEAR B FOR SIGN
1017:	0444  CD1105  	                CALL    CHKSGN  ;CHECK SIGN OF 2ND
1018:	0447  E3      	                EX      (SP),HL ;GET 1ST IN HL
1019:	0448  CD1105  	                CALL    CHKSGN  ;CHECK SIGN OF 1ST
1020:	044B  EB      	                EX      DE,HL
1021:	044C  E3      	                EX      (SP),HL
1022:	044D  EB      	                EX      DE,HL
1023:	044E  7A      	                LD      A,D     ;DIVIDE BY 0?
1024:	044F  B3      	                OR      E
1025:	0450  CAD100  	                JP      Z,AHOW  ;SAY "HOW?"
1026:	0453  C5      	                PUSH    BC      ;ELSE SAVE SIGN
1027:	0454  CDF804  	                CALL    DIVIDE  ;USE SUBROUTINE
1028:	0457  60      	                LD      H,B     ;RESULT IN HL NOW
1029:	0458  69      	                LD      L,C
1030:	0459  C1      	                POP     BC      ;GET SIGN BACK
1031:	045A  D1      	XP35:           POP     DE      ;AND TEXT POINTER
1032:	045B  7C      	                LD      A,H     ;HL MUST BE +
1033:	045C  B7      	                OR      A
1034:	045D  FAD000  	                JP      M,QHOW  ;ELSE IT IS OVERFLOW
1035:	0460  78      	                LD      A,B
1036:	0461  B7      	                OR      A
1037:	0462  FC1405  	                CALL    M,CHGSGN        ;CHANGE SIGN IF NEEDED
1038:	0465  18A8    	                JR      XP31    ;LOOK FOR MORE TERMS
1039:			;
1040:	0467  218807  	EXPR4:          LD      HL,TAB4-1       ;FIND FUNCTION IN TAB4
1041:	046A  C3F506  	                JP      EXEC    ;AND GO DO IT
1042:			;
1043:	046D  FF      	XP40:           RST     RTSTV   ;NO, NOT A FUNCTION
1044:	046E  3805    	                JR      C,XP41  ;NOR A VARIABLE
1045:	0470  7E      	                LD      A,(HL)  ;VARIABLE
1046:	0471  23      	                INC     HL
1047:	0472  66      	                LD      H,(HL)  ;VALUE IN HL
1048:	0473  6F      	                LD      L,A
1049:	0474  C9      	                RET
1050:			;
1051:	0475  CD7E00  	XP41:           CALL    TSTNUM  ;OR IS IT A NUMBER
1052:	0478  78      	                LD      A,B     ;# OF DIGIT
1053:	0479  B7      	                OR      A
1054:	047A  C0      	                RET     NZ      ;OK
1055:	047B  CF2805  	PARN:           TSTC    $28,XP43 ; '('
1056:	047E  DF      	                RST     REXPR   ;"(EXPR)"
1057:	047F  CF2901  	                TSTC    $29,XP43 ; ')'
1058:	0482  C9      	XP42:           RET
1059:	0483  C35305  	XP43:           JP      QWHAT   ;ELSE SAY: "WHAT?"
1060:			
1061:	0486  CD7B04  	RND:            CALL    PARN    ;*** RND(EXPR) ***
1062:	0489  7C      	                LD      A,H     ;EXPR MUST BE +
1063:	048A  B7      	                OR      A
1064:	048B  FAD000  	                JP      M,QHOW
1065:	048E  B5      	                OR      L       ;AND NON-ZERO
1066:	048F  CAD000  	                JP      Z,QHOW
1067:	0492  D5      	                PUSH    DE      ;SAVE BOTH
1068:	0493  E5      	                PUSH    HL
1069:	0494  2A4A08  	                LD      HL,(RANPNT)     ;GET MEMORY AS RANDOM
1070:	0497  11E007  	                LD      DE,LSTROM       ;NUMBER
1071:	049A  E7      	                RST     RCOMP
1072:	049B  3803    	                JR      C,RA1   ;WRAP AROUND IF LAST
1073:	049D  210000  	                LD      HL,CSTART
1074:	04A0  ED5F    	RA1:            LD      A,R     ;RFSH REG GIVES TIME DEP. RANDOM
1075:	04A2  AE      	                XOR     (HL)
1076:	04A3  5F      	                LD      E,A
1077:	04A4  23      	                INC     HL
1078:	04A5  AE      	                XOR     (HL)
1079:	04A6  57      	                LD      D,A
1080:	04A7  224A08  	                LD      (RANPNT),HL
1081:	04AA  E1      	                POP     HL
1082:	04AB  EB      	                EX      DE,HL
1083:	04AC  C5      	                PUSH    BC
1084:	04AD  CDF804  	                CALL    DIVIDE  ;RND(N)=MOD(M,N)+1
1085:	04B0  C1      	                POP     BC
1086:	04B1  D1      	                POP     DE
1087:	04B2  23      	                INC     HL
1088:	04B3  C9      	                RET
1089:			
1090:	04B4  CD7B04  	ABS:            CALL    PARN    ;*** ABS(EXPR) ***
1091:	04B7  1B      	                DEC     DE
1092:	04B8  CD1105  	                CALL    CHKSGN  ;CHECK SIGN AND CHANGE IF HL < 0
1093:	04BB  13      	                INC     DE
1094:	04BC  C9      	                RET
1095:			
1096:	04BD  2A3608  	SIZE:           LD      HL,(TXTUNF)     ;*** RETURN SIZE IN HL ***
1097:	04C0  D5      	                PUSH    DE              ;GET THE NUMBER OF FREE
1098:	04C1  EB      	                EX      DE,HL           ;BYTES BETWEEN 'TXTUNF'
1099:	04C2  21000F  	                LD      HL,TXTEND       ;AND 'TXTEND'
1100:	04C5  CD0D05  	                CALL    SUBDE
1101:	04C8  D1      	                POP     DE
1102:	04C9  C9      	                RET
1103:			
1104:	04CA  CD7B04  	GET:            CALL    PARN    ;*** GET(ADDR) ***
1105:	04CD  6E      	                LD      L,(HL)  ;GET CONTENT OF (HL)
1106:	04CE  2600    	                LD      H,0     ;RETURN RESULT IN HL
1107:	04D0  C9      	                RET
1108:			
1109:	04D1  CD7B04  	USR:            CALL    PARN    ;*** USR(PARA) ***
1110:	04D4  C3000F  	                JP      USRSPC  ;GET para in HL and JP to prog
1111:			;                               ;There you should:
1112:			;               ...             ;    - Do the work
1113:			;               ...             ;    - Put result in HL
1114:			;               RET             ;$C9 - RET to BASIC
1115:			;                               ;DEFAULT: (USRSPC)=$C9
1116:			
1117:	04D7  210008  	RAM:            LD      HL,RAMBGN ; *** RAM *** VARIABLES 'A'..'Z'
1118:	04DA  C9      	                RET
1119:			
1120:	04DB  210009  	TXT:            LD      HL,TXTBGN ; *** TXT *** BEGIN OF TXT AREA
1121:	04DE  C9      	                RET
1122:			
1123:	04DF  21000F  	TOP:            LD      HL,TXTEND ; *** TOP *** END OF TEXT AREA = USRSPC
1124:	04E2  C9      	                RET
1125:			
1126:	04E3  DF      	PUT:            RST     REXPR   ;*** PUT ADDR, VAL1 [,VAL2, VAL3,..]
1127:	04E4  CF2C0D  	                TSTC    $2C,PT2  ; 1ST ',' SEPARATES THE VALUE(S)
1128:	04E7  E5      	                PUSH    HL      ;SAVE ADDR
1129:	04E8  DF      	PT0:            RST     REXPR   ;GET VAL IN HL
1130:	04E9  7D      	                LD      A,L     ;LOW BYTE OF VAL
1131:	04EA  E1      	                POP     HL      ;GET ADDR
1132:	04EB  77      	                LD      (HL),A  ;PUT VALUE IN RAM
1133:	04EC  CF2C04  	                TSTC    $2C,PT1 ;READY UNLESS ","
1134:	04EF  23      	                INC     HL      ;NEXT ADDR
1135:	04F0  E5      	                PUSH    HL
1136:	04F1  18F5    	                JR      PT0     ;LIST CONTINUES
1137:			;
1138:	04F3  F7      	PT1:            RST     RFINISH ;READY
1139:			;
1140:	04F4  C35305  	PT2:            JP      QWHAT   ;ELSE SAY: "WHAT?"
1141:			
1142:	04F7  76      	HALT_:          HALT            ;HALT CPU (return to analyser)
1143:			
1144:			;
1145:			;*************************************************************
1146:			;
1147:			; *** DIVIDE *** SUBDE *** CHKSGN *** CHGSGN *** & CKHLDE ***
1148:			;
1149:			; 'DIVIDE' DIVIDES HL BY DE, RESULT IN BC, REMAINDER IN HL
1150:			;
1151:			; 'SUBDE' SUBSTRACTS DE FROM HL
1152:			;
1153:			; 'CHKSGN' CHECKS SIGN OF HL.  IF +, NO CHANGE.  IF -, CHANGE
1154:			; SIGN AND FLIP SIGN OF B.
1155:			;
1156:			; 'CHGSGN' CHECKS SIGN N OF HL AND B UNCONDITIONALLY.
1157:			;
1158:			; 'CKHLDE' CHECKS SIGN OF HL AND DE.  IF DIFFERENT, HL AND DE
1159:			; ARE INTERCHANGED.  IF SAME SIGN, NOT INTERCHANGED.  EITHER
1160:			; CASE, HL DE ARE THEN COMPARED TO SET THE FLAGS.
1161:			;
1162:	04F8  E5      	DIVIDE:         PUSH    HL      ;*** DIVIDE ***
1163:	04F9  6C      	                LD      L,H     ;DIVIDE H BY DE
1164:	04FA  2600    	                LD      H,0
1165:	04FC  CD0305  	                CALL    DV1
1166:	04FF  41      	                LD      B,C     ;SAVE RESULT IN B
1167:	0500  7D      	                LD      A,L     ;(REMINDER+L)/DE
1168:	0501  E1      	                POP     HL
1169:	0502  67      	                LD      H,A
1170:	0503  0EFF    	DV1:            LD      C,0FFH  ;RESULT IN C
1171:	0505  0C      	DV2:            INC     C       ;DUMB ROUTINE
1172:	0506  CD0D05  	                CALL    SUBDE   ;DIVIDE BY SUBTRACT
1173:	0509  30FA    	                JR      NC,DV2  ;AND COUNT
1174:	050B  19      	                ADD     HL,DE
1175:	050C  C9      	                RET
1176:			
1177:	050D  B7      	SUBDE:          OR      A       ;CLR CY
1178:	050E  ED52    	                SBC     HL,DE
1179:	0510  C9      	                RET
1180:			
1181:	0511  7C      	CHKSGN:         LD      A,H     ;*** CHKSGN ***
1182:	0512  B7      	                OR      A       ;CHECK SIGN OF HL
1183:	0513  F0      	                RET     P       ;IF HL >=0 RETURN
1184:			;
1185:	0514  7C      	CHGSGN:         LD      A,H     ;*** CHGSGN ***
1186:	0515  B5      	                OR      L       ;CHECK VALUE OF HL
1187:	0516  C8      	                RET     Z       ;IF HL == 0 RETURN
1188:			;
1189:	0517  7C      	                LD      A,H
1190:	0518  F5      	                PUSH    AF      ;SAVE SIGN
1191:	0519  2F      	                CPL             ;CHANGE SIGN OF HL
1192:	051A  67      	                LD      H,A
1193:	051B  7D      	                LD      A,L
1194:	051C  2F      	                CPL
1195:	051D  6F      	                LD      L,A
1196:	051E  23      	                INC     HL      ;HL = -HL
1197:	051F  F1      	                POP     AF      ;GET ORIGINAL SIGN
1198:	0520  AC      	                XOR     H       ;COMPARE
1199:	0521  F2D000  	                JP      P,QHOW  ;ERROR IF SIGN UNCHANGED (HL=$8000)
1200:	0524  78      	                LD      A,B     ;AND ALSO FLIP B
1201:	0525  EE80    	                XOR     80H
1202:	0527  47      	                LD      B,A
1203:	0528  C9      	                RET
1204:			
1205:	0529  7C      	CKHLDE:         LD      A,H
1206:	052A  AA      	                XOR     D       ;SAME SIGN?
1207:	052B  F22F05  	                JP      P,CK1   ;YES, COMPARE
1208:	052E  EB      	                EX      DE,HL   ;NO, XCH AND COMP
1209:	052F  E7      	CK1:            RST     RCOMP
1210:	0530  C9      	                RET
1211:			;
1212:			;*************************************************************
1213:			;
1214:			; *** SETVAL *** FIN *** ENDCHK *** & ERROR (& FRIENDS) ***
1215:			;
1216:			; "SETVAL" EXPECTS A VARIABLE, FOLLOWED BY AN EQUAL SIGN AND
1217:			; THEN AN EXPR.  IT EVALUATES THE EXPR. AND SET THE VARIABLE
1218:			; TO THAT VALUE.
1219:			;
1220:			; "FIN" CHECKS THE END OF A COMMAND.  IF IT ENDED WITH ";",
1221:			; EXECUTION CONTINUES.  IF IT ENDED WITH A CR, IT FINDS THE
1222:			; NEXT LINE AND CONTINUE FROM THERE.
1223:			;
1224:			; "ENDCHK" CHECKS IF A COMMAND IS ENDED WITH CR.  THIS IS
1225:			; REQUIRED IN CERTAIN COMMANDS.  (GOTO, RETURN, AND STOP ETC.)
1226:			;
1227:			; "ERROR" PRINTS THE STRING POINTED BY DE (AND ENDS WITH CR).
1228:			; IT THEN PRINTS THE LINE POINTED BY 'CURRNT' WITH A "?"
1229:			; INSERTED AT WHERE THE OLD TEXT POINTER (SHOULD BE ON TOP
1230:			; OF THE STACK) POINTS TO.  EXECUTION OF TB IS STOPPED
1231:			; AND TBI IS RESTARTED.  HOWEVER, IF 'CURRNT' -> ZERO
1232:			; (INDICATING A DIRECT COMMAND), THE DIRECT COMMAND IS NOT
1233:			; PRINTED.  AND IF 'CURRNT' -> NEGATIVE # (INDICATING 'INPUT'
1234:			; COMMAND), THE INPUT LINE IS NOT PRINTED AND EXECUTION IS
1235:			; NOT TERMINATED BUT CONTINUED AT 'INPERR'.
1236:			;
1237:			; RELATED TO 'ERROR' ARE THE FOLLOWING:
1238:			; 'QWHAT' SAVES TEXT POINTER IN STACK AND GET MESSAGE "WHAT?"
1239:			; 'AWHAT' JUST GET MESSAGE "WHAT?" AND JUMP TO 'ERROR'.
1240:			; 'QSORRY' AND 'ASORRY' DO SAME KIND OF THING.
1241:			; 'AHOW' AND 'AHOW' IN THE ZERO PAGE SECTION ALSO DO THIS.
1242:			;
1243:	0531  FF      	SETVAL:         RST     RTSTV   ;*** SETVAL ***
1244:	0532  381F    	                JR      C,QWHAT ;"WHAT?" NO VARIABLE
1245:	0534  E5      	                PUSH    HL      ;SAVE ADDRESS OF VAR.
1246:	0535  CF3D1B  	                TSTC    '=',QWHAT ;PASS "=" SIGN
1247:	0538  DF      	                RST     REXPR   ;EVALUATE EXPR.
1248:	0539  44      	                LD      B,H     ;VALUE IS IN BC NOW
1249:	053A  4D      	                LD      C,L
1250:	053B  E1      	                POP     HL      ;GET ADDRESS
1251:	053C  71      	                LD      (HL),C  ;SAVE VALUE
1252:	053D  23      	                INC     HL
1253:	053E  70      	                LD      (HL),B
1254:	053F  C9      	                RET
1255:			
1256:	0540  CF3B04  	FIN:            TSTC    ';',FI1 ;*** FIN ***
1257:	0543  F1      	                POP     AF      ;";", PURGE RET. ADDR.
1258:	0544  C3B801  	                JP      RUNSML  ;CONTINUE SAME LINE
1259:	0547  CF0D04  	FI1:            TSTC    CR,FI2  ;NOT ";", IS IT CR?
1260:	054A  F1      	                POP     AF      ;YES, PURGE RET. ADDR.
1261:	054B  C3A801  	                JP      RUNNXL  ;RUN NEXT LINE
1262:	054E  C9      	FI2:            RET             ;ELSE RETURN TO CALLER
1263:			
1264:	054F  EF      	ENDCHK:         RST     RIGNBLK ;IGNBLK
1265:	0550  FE0D    	                CP      CR      ;END WITH CR?
1266:	0552  C8      	                RET     Z       ;OK, ELSE SAY: "WHAT?"
1267:			;
1268:	0553  D5      	QWHAT:          PUSH    DE      ;*** QWHAT ***
1269:	0554  11E900  	AWHAT:          LD      DE,WHAT ;*** AWHAT ***
1270:	0557  97      	ERROR:          SUB     A       ;*** ERROR ***
1271:	0558  CDEA05  	                CALL    PRTSTG  ;PRINT 'WHAT?', 'HOW?'
1272:	055B  D1      	                POP     DE      ;OR 'SORRY'
1273:	055C  1A      	                LD      A,(DE)  ;SAVE THE CHARACTER
1274:	055D  F5      	                PUSH    AF      ;AT WHERE OLD DE ->
1275:	055E  97      	                SUB     A       ;AND PUT A 0 THERE
1276:	055F  12      	                LD      (DE),A
1277:	0560  2A3808  	                LD      HL,(CURRNT)     ;GET CURRENT LINE #
1278:	0563  E5      	                PUSH    HL
1279:	0564  7E      	                LD      A,(HL)  ;CHECK THE VALUE
1280:	0565  23      	                INC     HL
1281:	0566  B6      	                OR      (HL)
1282:	0567  D1      	                POP     DE
1283:	0568  CA1E01  	                JP      Z,WSTART ;IF ZERO, JUST RESTART
1284:	056B  7E      	                LD      A,(HL)  ;IF NEGATIVE,
1285:	056C  B7      	                OR      A
1286:	056D  FA3403  	                JP      M,INPERR        ;REDO INPUT
1287:	0570  CD8306  	                CALL    PRTLN   ;ELSE PRINT THE LINE
1288:	0573  1B      	                DEC     DE      ;UPTO WHERE THE 0 IS
1289:	0574  F1      	                POP     AF      ;RESTORE THE CHARACTER
1290:	0575  12      	                LD      (DE),A
1291:	0576  3E3F    	                LD      A,'?'   ;PRINT A "?"
1292:	0578  D7      	                RST     ROUTC
1293:	0579  97      	                SUB     A       ;AND THE REST OF THE
1294:	057A  CDEA05  	                CALL    PRTSTG  ;LINE
1295:	057D  C31E01  	                JP      WSTART  ;THEN RESTART
1296:			;
1297:	0580  D5      	QSORRY:         PUSH    DE      ;*** QSORRY ***
1298:	0581  11EF00  	ASORRY:         LD      DE,SORRY        ;*** ASORRY ***
1299:	0584  18D1    	                JR      ERROR
1300:			
1301:			;
1302:			;*************************************************************
1303:			;
1304:			; *** GETLN *** FNDLN (& FRIENDS) ***
1305:			;
1306:			; 'GETLN' READS A INPUT LINE INTO 'BUFFER'.  IT FIRST PROMPT
1307:			; THE CHARACTER IN A (GIVEN BY THE CALLER), THEN IT FILLS
1308:			; THE BUFFER AND ECHOS.  IT IGNORES LF'S AND NULLS, BUT STILL
1309:			; ECHOS THEM BACK.  RUB-OUT IS USED TO CAUSE IT TO DELETE
1310:			; THE LAST CHARACTER (IF THERE IS ONE), AND ALT-MOD IS USED TO
1311:			; CAUSE IT TO DELETE THE WHOLE LINE AND START IT ALL OVER.
1312:			; CR SIGNALS THE END OF A LINE, AND CAUSE 'GETLN' TO RETURN.
1313:			;
1314:			; 'FNDLN' FINDS A LINE WITH A GIVEN LINE # (IN HL) IN THE
1315:			; TEXT SAVE AREA.  DE IS USED AS THE TEXT POINTER.  IF THE
1316:			; LINE IS FOUND, DE WILL POINT TO THE BEGINNING OF THAT LINE
1317:			; (I.E., THE LOW BYTE OF THE LINE #), AND FLAGS ARE NC & Z.
1318:			; IF THAT LINE IS NOT THERE AND A LINE WITH A HIGHER LINE #
1319:			; IS FOUND, DE POINTS TO THERE AND FLAGS ARE NC & NZ.  IF
1320:			; WE REACHED THE END OF TEXT SAVE AREA AND CANNOT FIND THE
1321:			; LINE, FLAGS ARE C & NZ.
1322:			; 'FNDLN' WILL INITIALIZE DE TO THE BEGINNING OF THE TEXT SAVE
1323:			; AREA TO START THE SEARCH.  SOME OTHER ENTRIES OF THIS
1324:			; ROUTINE WILL NOT INITIALIZE DE AND DO THE SEARCH.
1325:			; 'FNDLNP' WILL START WITH DE AND SEARCH FOR THE LINE #.
1326:			; 'FNDNXT' WILL BUMP DE BY 2, FIND A CR AND THEN START SEARCH.
1327:			; 'FNDSKP' USE DE TO FIND A CR, AND THEN START SEARCH.
1328:			;
1329:	0586  D7      	GETLN:          RST     ROUTC   ;*** GETLN ***
1330:	0587  11800F  	                LD      DE,BUFFER       ;PROMPT AND INIT.
1331:	058A  CDFB00  	GL1:            CALL    CHKIO   ;CHECK KEYBOARD
1332:	058D  28FB    	                JR      Z,GL1   ;NO INPUT, WAIT
1333:	058F  FE08    	                CP      BS      ;BS, DELETE LAST CHARACTER?
1334:	0591  281A    	                JR      Z,GL3   ;YES
1335:	0593  FE7F    	                CP      DEL     ;DEL, DELETE LAST CHARACTER?
1336:	0595  2816    	                JR      Z,GL3   ;YES
1337:	0597  D7      	                RST     ROUTC   ;INPUT, ECHO BACK
1338:	0598  FE0A    	                CP      LF      ;IGNORE LF
1339:	059A  28EE    	                JR      Z,GL1
1340:	059C  B7      	                OR      A       ;IGNORE NULL
1341:	059D  28EB    	                JR      Z,GL1
1342:	059F  FE18    	                CP      CAN     ;^X, DELETE THE WHOLE LINE?
1343:	05A1  281B    	                JR      Z,GL4   ;YES
1344:	05A3  12      	                LD      (DE),A  ;ELSE SAVE INPUT
1345:	05A4  13      	                INC     DE      ;AND BUMP POINTER
1346:	05A5  FE0D    	                CP      CR      ;WAS IT CR?
1347:	05A7  C8      	                RET     Z       ;YES, END OF LINE
1348:	05A8  7B      	                LD      A,E     ;ELSE MORE FREE ROOM?
1349:	05A9  FEFF    	                CP      BUFEND & 0FFH
1350:	05AB  20DD    	                JR      NZ,GL1  ;YES, GET NEXT INPUT
1351:	05AD  7B      	GL3:            LD      A,E     ;DELETE LAST CHARACTER
1352:	05AE  FE80    	                CP      BUFFER & 0FFH   ;BUT DO WE HAVE ANY?
1353:	05B0  280C    	                JR      Z,GL4   ;NO, REDO WHOLE LINE
1354:	05B2  1B      	                DEC     DE      ;YES, BACKUP POINTER
1355:	05B3  3E08    	                LD      A,BS    ;AND ECHO A BACKSPACE
1356:	05B5  D7      	                RST     ROUTC
1357:	05B6  3E20    	                LD      A,' '   ;AND ECHO A BLANK
1358:	05B8  D7      	                RST     ROUTC
1359:	05B9  3E08    	                LD      A,BS    ;AND ECHO A BACKSPACE
1360:	05BB  D7      	                RST     ROUTC
1361:	05BC  18CC    	                JR      GL1     ;GO GET NEXT INPUT
1362:	05BE  CD0E00  	GL4:            CALL    CRLF    ;REDO ENTIRE LINE
1363:	05C1  3E5E    	                LD      A,'^'   ;CR, LF AND UP-ARROW
1364:	05C3  18C1    	                JR      GETLN
1365:			;
1366:	05C5  7C      	FNDLN:          LD      A,H     ;*** FNDLN ***
1367:	05C6  B7      	                OR      A       ;CHECK SIGN OF HL
1368:	05C7  FAD000  	                JP      M,QHOW  ;IT CANNOT BE -
1369:	05CA  110009  	                LD      DE,TXTBGN       ;INIT TEXT POINTER
1370:			;
1371:	05CD          	FNDLP:          ;*** FDLNP ***
1372:	05CD  E5      	FL1:            PUSH    HL      ;SAVE LINE #
1373:	05CE  2A3608  	                LD      HL,(TXTUNF)     ;CHECK IF WE PASSED END
1374:	05D1  2B      	                DEC     HL
1375:	05D2  E7      	                RST     RCOMP
1376:	05D3  E1      	                POP     HL      ;GET LINE # BACK
1377:	05D4  D8      	                RET     C       ;C,NZ PASSED END
1378:	05D5  1A      	                LD      A,(DE)  ;WE DID NOT, GET BYTE 1
1379:	05D6  95      	                SUB     L       ;IS THIS THE LINE?
1380:	05D7  47      	                LD      B,A     ;COMPARE LOW ORDER
1381:	05D8  13      	                INC     DE
1382:	05D9  1A      	                LD      A,(DE)  ;GET BYTE 2
1383:	05DA  9C      	                SBC     A,H     ;COMPARE HIGH ORDER
1384:	05DB  3804    	                JR      C,FL2   ;NO, NOT THERE YET
1385:	05DD  1B      	                DEC     DE      ;ELSE WE EITHER FOUND
1386:	05DE  B0      	                OR      B       ;IT, OR IT IS NOT THERE
1387:	05DF  C9      	                RET     ;NC,Z:FOUND, NC,NZ:NO
1388:			;
1389:	05E0          	FNDNXT:         ;*** FNDNXT ***
1390:	05E0  13      	                INC     DE      ;FIND NEXT LINE
1391:	05E1  13      	FL2:            INC     DE      ;JUST PASSED BYTE 1 & 2
1392:			;
1393:	05E2  1A      	FNDSKP:         LD      A,(DE)  ;*** FNDSKP ***
1394:	05E3  FE0D    	                CP      CR      ;TRY TO FIND CR
1395:	05E5  20FA    	                JR      NZ,FL2  ;KEEP LOOKING
1396:	05E7  13      	                INC     DE      ;FOUND CR, SKIP OVER
1397:	05E8  18E3    	                JR      FL1     ;CHECK IF END OF TEXT
1398:			;
1399:			;*************************************************************
1400:			;
1401:			; *** PRTSTG *** QTSTG *** PRTNUM *** & PRTLN ***
1402:			;
1403:			; 'PRTSTG' PRINTS A STRING POINTED BY DE.  IT STOPS PRINTING
1404:			; AND RETURNS TO CALLER WHEN EITHER A CR IS PRINTED OR WHEN
1405:			; THE NEXT BYTE IS THE SAME AS WHAT WAS IN A (GIVEN BY THE
1406:			; CALLER).  OLD A IS STORED IN B, OLD B IS LOST.
1407:			;
1408:			; 'QTSTG' LOOKS FOR A BACK-ARROW, SINGLE QUOTE, OR DOUBLE
1409:			; QUOTE.  IF NONE OF THESE, RETURN TO CALLER.  IF BACK-ARROW,
1410:			; OUTPUT A CR WITHOUT A LF.  IF SINGLE OR DOUBLE QUOTE, PRINT
1411:			; THE STRING IN THE QUOTE AND DEMANDS A MATCHING UNQUOTE.
1412:			; HACK AFTER THE PRINTING THE NEXT 3 BYTES OF THE CALLER
1413:			;      IS SKIPPED OVER (SHALL BE A "JP" INSTRUCTION).
1414:			;
1415:			; 'PRTNUM' PRINTS THE NUMBER IN HL.  LEADING BLANKS ARE ADDED
1416:			; IF NEEDED TO PAD THE NUMBER OF SPACES TO THE NUMBER IN C.
1417:			; HOWEVER, IF THE NUMBER OF DIGITS IS LARGER THAN THE # IN
1418:			; C, ALL DIGITS ARE PRINTED ANYWAY.  NEGATIVE SIGN IS ALSO
1419:			; PRINTED AND COUNTED IN, POSITIVE SIGN IS NOT.
1420:			;
1421:			; 'PRTLN' PRINTS A SAVED TEXT LINE WITH LINE # AND ALL.
1422:			;
1423:	05EA  47      	PRTSTG:         LD      B,A     ;*** PRTSTG ***
1424:	05EB  1A      	PS1:            LD      A,(DE)  ;GET A CHARACTER
1425:	05EC  13      	                INC     DE      ;BUMP POINTER
1426:	05ED  B8      	                CP      B       ;SAME AS OLD A?
1427:	05EE  C8      	                RET     Z       ;YES, RETURN
1428:	05EF  D7      	                RST     ROUTC   ;ELSE PRINT IT
1429:	05F0  FE0D    	                CP      CR      ;WAS IT A CR?
1430:	05F2  20F7    	                JR      NZ,PS1  ;NO, NEXT
1431:	05F4  C9      	                RET             ;YES, RETURN
1432:			;
1433:	05F5  CF220F  	QTSTG:          TSTC    $22,QT3 ;*** QTSTG ***
1434:	05F8  3E22    	                LD      A,22H   ;IT IS A '"'
1435:	05FA  CDEA05  	QT1:            CALL    PRTSTG  ;PRINT UNTIL ANOTHER
1436:	05FD  FE0D    	QT1A:           CP      CR      ;WAS LAST ONE A CR?
1437:	05FF  E1      	                POP     HL       ; HACK RETURN ADDRESS
1438:	0600  CAA801  	                JP      Z,RUNNXL ; WAS CR, RUN NEXT LINE
1439:	0603  23      	QT2:            INC     HL       ; !! SKIP 3 BYTES ON RETURN
1440:	0604  23      	                INC     HL       ; !! -> AFTER "CALL QTSTG"
1441:	0605  23      	                INC     HL       ; !!    MUST BE "JP .."
1442:	0606  E9      	                JP      (HL)     ; !! RETURN AFTER THIS "JP"
1443:			;
1444:	0607  CF2704  	QT3:            TSTC    $27,QT4 ;IS IT A "'"?
1445:	060A  3E27    	                LD      A,27H   ;YES, DO THE SAME
1446:	060C  18EC    	                JR      QT1     ;AS IN '"'
1447:			;
1448:	060E  CF5F06  	QT4:            TSTC    $5F,QT5 ;IS IT UNDERLINE?
1449:	0611  3E8D    	                LD      A,08DH  ;YES, CR WITHOUT LF
1450:	0613  D7      	                RST     ROUTC
1451:	0614  E1      	                POP     HL      ;HACK RETURN ADDRESS
1452:	0615  18EC    	                JR      QT2
1453:			;
1454:	0617  CF5E08  	QT5:            TSTC    5EH,QT6 ;RST 1, is it '^'?
1455:	061A  1A      	                LD      A,(DE)  ;CHR
1456:	061B  EE40    	                XOR     40H     ;CONVERT TO CTRL
1457:	061D  D7      	                RST     ROUTC
1458:	061E  1A      	                LD      A,(DE)  ;RESTORE CHR
1459:	061F  13      	                INC     DE
1460:	0620  18DB    	                JR      QT1A
1461:	0622  C9      	QT6:            RET             ;NONE OF ABOVE
1462:			
1463:	0623          	PRTNUM:                         ;*** PRINT NUMBER IN HL ***
1464:	0623  3A3508  	                LD      A,(PNBASE)      ;GET NUMBER BASE
1465:	0626  B7      	                OR      A
1466:	0627  2809    	                JR      Z,PN0   ;0: DEFAULT DEC
1467:	0629  FE10    	                CP      16      ;HEX NUMBER?
1468:	062B  2010    	                JR      NZ,PN1  ;NO
1469:	062D  0624    	                LD      B,'$'   ;PRINT LEADING '$'
1470:	062F  0D      	                DEC     C       ;'$' TAKES SPACE
1471:	0630  180B    	                JR      PN1     ;HEX IS UNSIGNED
1472:	0632  0600    	PN0:            LD      B,0     ;NO PREFIX YET
1473:	0634  CD1105  	                CALL    CHKSGN  ;CHECK SIGN
1474:	0637  F23D06  	                JP      P,PN1   ;NO SIGN
1475:	063A  062D    	                LD      B,'-'   ;B=SIGN
1476:	063C  0D      	                DEC     C       ;'-' TAKES SPACE
1477:	063D  D5      	PN1:            PUSH    DE
1478:	063E  3A3508  	                LD      A,(PNBASE)
1479:	0641  B7      	                OR      A       ;DEFAULT DECIMAL?
1480:	0642  2002    	                JR      NZ,PN1A
1481:	0644  3E0A    	                LD      A,10
1482:	0646  5F      	PN1A:           LD      E,A
1483:	0647  AF      	                XOR     A
1484:	0648  57      	                LD      D,A
1485:	0649  D5      	                PUSH    DE      ;SAVE AS A FLAG
1486:	064A  0D      	                DEC     C       ;C=SPACES
1487:	064B  C5      	                PUSH    BC      ;SAVE SIGN & SPACE
1488:	064C  CDF804  	PN2:            CALL    DIVIDE  ;DIVIDE HL BY NUMBER BASE
1489:	064F  78      	                LD      A,B     ;RESULT 0?
1490:	0650  B1      	                OR      C
1491:	0651  2807    	                JR      Z,PN3   ;YES, WE GOT ALL
1492:	0653  E3      	                EX      (SP),HL ;NO, SAVE REMAINDER
1493:	0654  2D      	                DEC     L       ;AND COUNT SPACE
1494:	0655  E5      	                PUSH    HL      ;HL IS OLD BC
1495:	0656  60      	                LD      H,B     ;MOVE RESULT TO BC
1496:	0657  69      	                LD      L,C
1497:	0658  18F2    	                JR      PN2     ;AND DIVIDE AGAIN
1498:			;
1499:	065A  C1      	PN3:            POP     BC      ;WE GOT ALL DIGITS IN
1500:	065B  0D      	PN4:            DEC     C       ;THE STACK
1501:	065C  79      	                LD      A,C     ;LOOK AT SPACE COUNT
1502:	065D  B7      	                OR      A
1503:	065E  FA6606  	                JP      M,PN5   ;NO LEADING BLANKS
1504:	0661  3E20    	                LD      A,' '   ;LEADING BLANKS
1505:	0663  D7      	                RST     ROUTC
1506:	0664  18F5    	                JR      PN4     ;MORE?
1507:	0666  78      	PN5:            LD      A,B     ;PRINT SIGN OR '$'
1508:	0667  B7      	                OR      A
1509:	0668  C41000  	                CALL    NZ,ROUTC
1510:	066B  5D      	                LD      E,L     ;LAST REMAINDER IN E
1511:	066C  3A3508  	PN6:            LD      A,(PNBASE)      ;GET NUMBER BASE
1512:	066F  B7      	                OR      A       ;DEFAULT DECIMAL?
1513:	0670  2002    	                JR      NZ,PN6A
1514:	0672  3E0A    	                LD      A,10
1515:	0674  BB      	PN6A:           CP      E       ;IT IS FLAG FOR NO MORE
1516:	0675  7B      	                LD      A,E     ;CHECK DIGIT IN E
1517:	0676  D1      	                POP     DE
1518:	0677  C8      	                RET     Z       ;IF SO, RETURN
1519:	0678  FE0A    	                CP      10      ;0-9? < A hex?
1520:	067A  3802    	                JR      C,PN7   ;Skip Add 7
1521:	067C  C607    	                ADD     A,'A'-'0'-10    ;Bring it up to ASCII A-F
1522:	067E  C630    	PN7:            ADD     A,'0'   ;ELSE CONVERT TO ASCII
1523:	0680  D7      	                RST     ROUTC   ;AND PRINT THE DIGIT
1524:	0681  18E9    	                JR      PN6     ;GO BACK FOR MORE
1525:			
1526:	0683  AF      	PRTLN:          XOR     A       ;0 -> DEFAULT BASE 10 SIGNED
1527:	0684  323508  	                LD      (PNBASE),A      ;FOR PRTNUM
1528:	0687  1A      	                LD      A,(DE)
1529:	0688  6F      	                LD      L,A     ;LOW ORDER LINE #
1530:	0689  13      	                INC     DE
1531:	068A  1A      	                LD      A,(DE)  ;HIGH ORDER
1532:	068B  67      	                LD      H,A
1533:	068C  13      	                INC     DE
1534:	068D  0E04    	                LD      C,4     ;PRINT 4 DIGIT LINE #
1535:	068F  CD2306  	                CALL    PRTNUM
1536:	0692  3E20    	                LD      A,' '   ;FOLLOWED BY A BLANK
1537:	0694  D7      	                RST     ROUTC
1538:	0695  97      	                SUB     A       ;AND THEN THE NEXT
1539:	0696  CDEA05  	                CALL    PRTSTG
1540:	0699  C9      	                RET
1541:			;
1542:			;*************************************************************
1543:			;
1544:			; *** MVUP *** MVDOWN *** POPA *** & PUSHA ***
1545:			;
1546:			; 'MVUP' MOVES A BLOCK UP FROM WHERE DE-> TO WHERE BC-> UNTIL
1547:			; DE = HL
1548:			;
1549:			; 'MVDOWN' MOVES A BLOCK DOWN FROM WHERE DE-> TO WHERE HL->
1550:			; UNTIL DE = BC
1551:			;
1552:			; 'POPA' RESTORES THE 'FOR' LOOP VARIABLE SAVE AREA FROM THE
1553:			; STACK
1554:			;
1555:			; 'PUSHA' STACKS THE 'FOR' LOOP VARIABLE SAVE AREA INTO THE
1556:			; STACK
1557:			;
1558:	069A  E7      	MVUP:           RST     RCOMP   ;*** MVUP ***
1559:	069B  C8      	                RET     Z       ;DE = HL, RETURN
1560:	069C  1A      	                LD      A,(DE)  ;GET ONE BYTE
1561:	069D  02      	                LD      (BC),A  ;MOVE IT
1562:	069E  13      	                INC     DE      ;INCREASE BOTH POINTERS
1563:	069F  03      	                INC     BC
1564:	06A0  18F8    	                JR      MVUP    ;UNTIL DONE
1565:			;
1566:	06A2  78      	MVDOWN:         LD      A,B     ;*** MVDOWN ***
1567:	06A3  92      	                SUB     D       ;TEST IF DE = BC
1568:	06A4  2003    	                JR      NZ,MD1  ;NO, GO MOVE
1569:	06A6  79      	                LD      A,C     ;MAYBE, OTHER BYTE?
1570:	06A7  93      	                SUB     E
1571:	06A8  C8      	                RET     Z       ;YES, RETURN
1572:	06A9  1B      	MD1:            DEC     DE      ;ELSE MOVE A BYTE
1573:	06AA  2B      	                DEC     HL      ;BUT FIRST DECREASE
1574:	06AB  1A      	                LD      A,(DE)  ;BOTH POINTERS AND
1575:	06AC  77      	                LD      (HL),A  ;THEN DO IT
1576:	06AD  18F3    	                JR      MVDOWN  ;LOOP BACK
1577:			;
1578:	06AF  C1      	POPA:           POP     BC      ;BC = RETURN ADDR.
1579:	06B0  E1      	                POP     HL      ;RESTORE LOPVAR, BUT
1580:	06B1  224008  	                LD      (LOPVAR),HL     ;=0 MEANS NO MORE
1581:	06B4  7C      	                LD      A,H
1582:	06B5  B5      	                OR      L
1583:	06B6  2810    	                JR      Z,PP1   ;YEP, GO RETURN
1584:	06B8  E1      	                POP     HL      ;NOP, RESTORE OTHERS
1585:	06B9  224208  	                LD      (LOPINC),HL
1586:	06BC  E1      	                POP     HL
1587:	06BD  224408  	                LD      (LOPLMT),HL
1588:	06C0  E1      	                POP     HL
1589:	06C1  224608  	                LD      (LOPLN),HL
1590:	06C4  E1      	                POP     HL
1591:	06C5  224808  	                LD      (LOPPT),HL
1592:	06C8  C5      	PP1:            PUSH    BC      ;BC = RETURN ADDR.
1593:	06C9  C9      	                RET
1594:			;
1595:	06CA  214C08  	PUSHA:          LD      HL,STKLMT       ;*** PUSHA ***
1596:	06CD  CD1405  	                CALL    CHGSGN
1597:	06D0  C1      	                POP     BC      ;BC=RETURN ADDRESS
1598:	06D1  39      	                ADD     HL,SP   ;IS STACK NEAR THE TOP?
1599:	06D2  D28005  	                JP      NC,QSORRY       ;YES, SORRY FOR THAT
1600:	06D5  2A4008  	                LD      HL,(LOPVAR)     ;ELSE SAVE LOOP VAR'S
1601:	06D8  7C      	                LD      A,H     ;BUT IF LOPVAR IS 0
1602:	06D9  B5      	                OR      L       ;THAT WILL BE ALL
1603:	06DA  2813    	                JR      Z,PU1
1604:	06DC  2A4808  	                LD      HL,(LOPPT)      ;ELSE, MORE TO SAVE
1605:	06DF  E5      	                PUSH    HL
1606:	06E0  2A4608  	                LD      HL,(LOPLN)
1607:	06E3  E5      	                PUSH    HL
1608:	06E4  2A4408  	                LD      HL,(LOPLMT)
1609:	06E7  E5      	                PUSH    HL
1610:	06E8  2A4208  	                LD      HL,(LOPINC)
1611:	06EB  E5      	                PUSH    HL
1612:	06EC  2A4008  	                LD      HL,(LOPVAR)
1613:	06EF  E5      	PU1:            PUSH    HL
1614:	06F0  C5      	                PUSH    BC      ;BC = RETURN ADDR.
1615:	06F1  C9      	                RET
1616:			
1617:			;*************************************************************
1618:			;
1619:			; *** DIRECT *** EXEC *** TABLES ***
1620:			;
1621:			; THIS SECTION OF THE CODE TESTS A STRING AGAINST A TABLE.
1622:			; WHEN A MATCH IS FOUND, CONTROL IS TRANSFERED TO THE SECTION
1623:			; OF CODE ACCORDING TO THE TABLE.
1624:			;
1625:			; AT 'EXEC', DE SHOULD POINT TO THE STRING AND HL SHOULD POINT
1626:			; TO THE TABLE-1.  AT 'DIRECT', DE SHOULD POINT TO THE STRING.
1627:			; HL WILL BE SET UP TO POINT TO TAB1-1, WHICH IS THE TABLE OF
1628:			; ALL DIRECT AND STATEMENT COMMANDS.
1629:			;
1630:			; A '.' IN THE STRING WILL TERMINATE THE TEST AND THE PARTIAL
1631:			; MATCH WILL BE CONSIDERED AS A MATCH.  E.G., 'P.', 'PR.',
1632:			; 'PRI.', 'PRIN.', OR 'PRINT' WILL ALL MATCH 'PRINT'.
1633:			;
1634:			
1635:	06F2  212607  	DIRECT:         LD      HL,TAB1-1       ;*** DIRECT ***
1636:			;
1637:	06F5          	EXEC:           ;*** EXEC ***
1638:	06F5  EF      	EX0:            RST     RIGNBLK ;IGNORE LEADING BLANKS
1639:	06F6  D5      	                PUSH    DE      ;SAVE POINTER
1640:	06F7  1A      	EX1:            LD      A,(DE)  ;IF FOUND '.' IN STRING
1641:	06F8  13      	                INC     DE      ;BEFORE ANY MISMATCH
1642:	06F9  FE2E    	                CP      2EH     ;WE DECLARE A MATCH
1643:	06FB  281C    	                JR      Z,EX3
1644:	06FD  FE61    	                CP      'a'     ;< 'a' ?
1645:	06FF  3806    	                JR      C,EXN   ;NO ALPHA CHAR
1646:	0701  FE7B    	                CP      'z'+1   ;> 'z'
1647:	0703  3002    	                JR      NC,EXN  ;NO ALPHA CHAR
1648:	0705  E65F    	                AND     5FH     ;MASK LOWER CASE TO UPPER CASE
1649:	0707          	EXN:
1650:	0707  23      	                INC     HL      ;HL->TABLE
1651:	0708  BE      	                CP      (HL)    ;IF MATCH, TEST NEXT
1652:	0709  28EC    	                JR      Z,EX1
1653:	070B  3E7F    	                LD      A,07FH  ;ELSE SEE IF BIT 7
1654:	070D  1B      	                DEC     DE      ;OF TABLE IS SET, WHICH
1655:	070E  BE      	                CP      (HL)    ;IS THE JUMP ADDR. (HI)
1656:	070F  380E    	                JR      C,EX5   ;C:YES, MATCHED
1657:	0711  23      	EX2:            INC     HL      ;NC:NO, FIND JUMP ADDR.
1658:	0712  BE      	                CP      (HL)
1659:	0713  30FC    	                JR      NC,EX2
1660:	0715  23      	                INC     HL      ;BUMP TO NEXT TAB. ITEM
1661:	0716  D1      	                POP     DE      ;RESTORE STRING POINTER
1662:	0717  18DC    	                JR      EX0     ;TEST AGAINST NEXT ITEM
1663:	0719  3E7F    	EX3:            LD      A,07FH  ;PARTIAL MATCH, FIND
1664:	071B  23      	EX4:            INC     HL      ;JUMP ADDR., WHICH IS
1665:	071C  BE      	                CP      (HL)    ;FLAGGED BY BIT 7
1666:	071D  30FC    	                JR      NC,EX4
1667:	071F  7E      	EX5:            LD      A,(HL)  ;LOAD HL WITH THE JUMP
1668:	0720  23      	                INC     HL      ;ADDRESS FROM THE TABLE
1669:	0721  6E      	                LD      L,(HL)
1670:			                                ;ADDRESSES ARE BIG-ENDIAN
1671:			                                ;WITH MSB SET to 1
1672:	0001          	                .IF      $ < 8000H
1673:	0722  E67F    	                AND     7FH     ;MASK OFF HIGH ADDRESS BIT
1674:			                .ENDIF
1675:	0724  67      	                LD      H,A
1676:	0725  F1      	                POP     AF      ;CLEAN UP THE GARBAGE
1677:	0726  E9      	                JP      (HL)    ;AND WE GO DO IT
1678:			;
1679:			
1680:			; THE TABLES CONSISTS OF ANY NUMBER OF ITEMS.  EACH ITEM
1681:			; IS A STRING OF CHARACTERS WITH BIT 7 SET TO 0 AND
1682:			; A JUMP ADDRESS STORED HI-LOW WITH BIT 7 OF THE HIGH
1683:			; BYTE SET TO 1.
1684:			; This is done by the macro 'DWA'.
1685:			; If the program is executed from an address < 0x8000
1686:			; take care to mask this bit in program part 'EXEC'.
1687:			;
1688:			; END OF TABLE IS AN ITEM WITH A JUMP ADDRESS ONLY.
1689:			; IF THE STRING DOES NOT MATCH ANY OF THE OTHER ITEMS,
1690:			; IT WILL MATCH THIS NULL ITEM AS DEFAULT.
1691:			
1692:			;
1693:	0727          	TAB1:           ;DIRECT ONLY COMMANDS
1694:	0727  4C495354	                .DB     "LIST"
1695:	072B  81CF    	                DWA     LIST_
1696:	072D  52554E  	                .DB     "RUN"
1697:	0730  81A2    	                DWA     RUN
1698:	0732  4E4557  	                .DB     "NEW"
1699:	0735  8193    	                DWA     NEW
1700:			;
1701:	0737          	TAB2:           ;DIRECT OR PROGRAM STATEMENT
1702:	0737  4E455854	                .DB     "NEXT"
1703:	073B  82CB    	                DWA     NEXT
1704:	073D  4C4554  	                .DB     "LET"           ; can be omitted
1705:	0740  838D    	                DWA     LET
1706:	0742  4946    	                .DB     "IF"
1707:	0744  8325    	                DWA     IF_
1708:	0746  474F544F	                .DB     "GOTO"
1709:	074A  81C1    	                DWA     GOTO
1710:	074C  474F5355	                .DB     "GOSUB"
	      42
1711:	0751  8237    	                DWA     GOSUB
1712:	0753  52455455	                .DB     "RETURN"
	      524E
1713:	0759  8257    	                DWA     RETURN
1714:	075B  52454D  	                .DB     "REM"
1715:	075E  8321    	                DWA     REM
1716:	0760  464F52  	                .DB     "FOR"
1717:	0763  8270    	                DWA     FOR
1718:	0765  494E5055	                .DB     "INPUT"         ; wait for KBD input
	      54
1719:	076A  833E    	                DWA     INPUT
1720:	076C  5052494E	                .DB     "PRINT"
	      54
1721:	0771  81E6    	                DWA     PRINT
1722:	0773  3F      	                .DB     "?"             ; short for PRINT
1723:	0774  81E6    	                DWA     PRINT
1724:	0776  505554  	                .DB     "PUT"           ; PUT ADDR, VAL, VAL,...
1725:	0779  84E3    	                DWA     PUT
1726:	077B  53544F50	                .DB     "STOP"          ; warm start
1727:	077F  819C    	                DWA     STOP
1728:	0781  48414C54	                .DB     "HALT"          ; HALT CPU (return to analyser)
1729:	0785  84F7    	                DWA     HALT_
1730:	0787  8388    	                DWA     DEFLT           ;END OF LIST
1731:			;
1732:	0789          	TAB4:           ;FUNCTIONS AND CONSTANTS
1733:	0789  524E44  	                .DB     "RND"           ;funct RND(RANGE)
1734:	078C  8486    	                DWA     RND
1735:	078E  414253  	                .DB     "ABS"           ;funct ABS(VALUE)
1736:	0791  84B4    	                DWA     ABS
1737:	0793  474554  	                .DB     "GET"           ;funct GET(ADR) get byte from memory
1738:	0796  84CA    	                DWA     GET
1739:	0798  555352  	                .DB     "USR"           ;funct USR(PARA) call usr funct at TOP
1740:	079B  84D1    	                DWA     USR                ; and return a result in HL
1741:	079D  53495A45	                .DB     "SIZE"          ;const SIZE - no parantesis, get free mem
1742:	07A1  84BD    	                DWA     SIZE
1743:	07A3  52414D  	                .DB     "RAM"           ;const RAM - no par., get RAM begin = 'A'..'Z'
1744:	07A6  84D7    	                DWA     RAM
1745:	07A8  545854  	                .DB     "TXT"           ;const TXT - no par., get TEXT begin
1746:	07AB  84DB    	                DWA     TXT
1747:	07AD  544F50  	                .DB     "TOP"           ;const TOP - no par., get TEXT TOP = USRSPC
1748:	07B0  84DF    	                DWA     TOP
1749:	07B2  846D    	                DWA     XP40            ;END OF LIST
1750:			;
1751:	07B4          	TAB5:           ;"TO" IN "FOR"
1752:	07B4  544F    	                .DB     "TO"
1753:	07B6  8280    	                DWA     FR1
1754:	07B8  8553    	                DWA     QWHAT           ;END OF LIST
1755:			;
1756:	07BA          	TAB6:           ;"STEP" IN "FOR"
1757:	07BA  53544550	                .DB     "STEP"
1758:	07BE  828A    	                DWA     FR2
1759:	07C0  828D    	                DWA     FR3             ;END OF LIST
1760:			;
1761:	07C2          	TAB8:           ;RELATION OPERATORS
1762:	07C2  3E3D    	                .DB     ">="
1763:	07C4  839C    	                DWA     XP11
1764:	07C6  213D    	                .DB     "!="
1765:	07C8  83A2    	                DWA     XP12
1766:	07CA  23      	                .DB     "#"
1767:	07CB  83A2    	                DWA     XP12
1768:	07CD  3E      	                .DB     ">"
1769:	07CE  83A8    	                DWA     XP13
1770:	07D0  3D3D    	                .DB     "=="
1771:	07D2  83B7    	                DWA     XP15
1772:	07D4  3D      	                .DB     "="
1773:	07D5  83B7    	                DWA     XP15
1774:	07D7  3C3D    	                .DB     "<="
1775:	07D9  83AF    	                DWA     XP14
1776:	07DB  3C      	                .DB     "<"
1777:	07DC  83BD    	                DWA     XP16
1778:	07DE  83C3    	                DWA     XP17            ;END OF REL OPERATOR LIST
1779:			
1780:			;
1781:	07E0          	LSTROM:                                 ;ALL ABOVE CAN BE ROM
1782:			
1783:			;
1784:			; CHECK THAT THE PROGRAM DOES NOT EXCEED ROM SIZE
1785:			;
1786:	0001          	                .ASSERT $ <= RAMBGN
1787:			
1788:			
1789:	07E0 ..07FF 00	                .DC     RAMBGN-$,$00    ;fill with $00 until RAMBGN
1790:			;
1791:			;
1792:			;*************************************************************
1793:			
1794:	0800          	                .ORG    RAMBGN          ;HERE DOWN MUST BE RAM
1795:			;
1796:			;*************************************************************
1797:			;
1798:			
1799:	0800          	VARBGN:         .DS     2*26            ;VARIABLES 'A'..'Z'
1800:	0834          	OCSW:           .DS     1               ;SWITCH FOR OUTPUT
1801:	0835          	PNBASE:         .DS     1               ;BASE FOR PRTNUM
1802:	0836          	TXTUNF:         .DS     2               ;->UNFILLED TEXT AREA
1803:	0838          	CURRNT:         .DS     2               ;POINTS TO CURRENT LINE
1804:	083A          	STKGOS:         .DS     2               ;SAVES SP IN 'GOSUB'
1805:	083C          	VARNXT:         .DS     2               ;TEMP STORAGE
1806:	083E          	STKINP:         .DS     2               ;SAVES SP IN 'INPUT'
1807:	0840          	LOPVAR:         .DS     2               ;'FOR' LOOP SAVE AREA
1808:	0842          	LOPINC:         .DS     2               ;INCREMENT
1809:	0844          	LOPLMT:         .DS     2               ;LIMIT
1810:	0846          	LOPLN:          .DS     2               ;LINE NUMBER
1811:	0848          	LOPPT:          .DS     2               ;TEXT POINTER
1812:	084A          	RANPNT:         .DS     2               ;RANDOM NUMBER POINTER
1813:	084C          	STKLMT:         .EQU    $               ;LIMIT FOR STACK
1814:			
1815:	0900          	                .ORG    RAMBGN+$100
1816:			;
1817:	0900          	STACK:                                  ;STACK STARTS HERE AND GROWS DOWN
1818:			
1819:	0900          	TXTBGN:                                 ;TEXT STARTS HERE AND GROWS UP
1820:			;
1821:			
1822:	0F00          	                .ORG    RAMBGN+RAMSZE-$100
1823:	0F00          	TXTEND:                                 ;TEXT SAVE AREA ENDS
1824:			;
1825:			                                        ;VARIABLES @(0), @(1), @(2)
1826:			                                        ;... stored top-down
1827:			                                        ;i.e. &@(i) = TXTEND-2-2*i
1828:			;
1829:	0F00          	USRSPC:         .DS     128
1830:			
1831:	0F80          	BUFFER:         .DS     127             ;INPUT BUFFER
1832:	0FFF          	BUFEND:         .DS     1               ;BUFFER END
1833:			
1834:	1000          	                .END



Statistics:

     4	passes
     0	jr promotions
   225	symbols
  2048	bytes

    64	macro calls
   205	macro bytes
     0	invented symbols



Symbol Table:

ABS              4B4      1204
AHOW            D1        209
ASORRY           581      1409
AWHAT            554      1364
BS             =08        8
BUFEND           FFF      4095
BUFFER           F80      3968
CAN            =18        24
CHGSGN           514      1300
CHKIO           FB        251
CHKSGN           511      1297
CI0              104      260
CK1              52F      1327
CKHLDE           529      1321
CR             =0D        13
CRLF            0E        14
CSTART          00        0
CURRNT           838      2104
DEFLT            388      904
DEL            =7F        127
DIRECT           6F2      1778
DIVIDE           4F8      1272
DV1              503      1283
DV2              505      1285
ENDCHK           54F      1359
ERROR            557      1367
EX0              6F5      1781
EX1              6F7      1783
EX2              711      1809
EX3              719      1817
EX4              71B      1819
EX5              71F      1823
EXEC             6F5      1781
EXN              707      1799
EXPR1            396      918
EXPR2            3DA      986
EXPR3            40C      1036
EXPR4            467      1127
FI1              547      1351
FI2              54E      1358
FIN              540      1344
FL1              5CD      1485
FL2              5E1      1505
FNDLN            5C5      1477
FNDLP            5CD      1485
FNDNXT           5E0      1504
FNDSKP           5E2      1506
FOR              270      624
FR1              280      640
FR2              28A      650
FR3              28D      653
FR4              290      656
FR5              293      659
FR7              2A8      680
FR8              2C6      710
GET              4CA      1226
GETLN            586      1414
GL1              58A      1418
GL3              5AD      1453
GL4              5BE      1470
GOSUB            237      567
GOTO             1C1      449
HALT_            4F7      1271
HOW             E1        225
IF_              325      805
INIT             109      265
INPERR           334      820
INPUT            33E      830
IODATA         =01        1
IOSTAT         =02        2
IO_RX_BIT      =01        1
IP1              33E      830
IP2              34A      842
IP3              35A      858
IP4              381      897
IP5              387      903
LET              38D      909
LF             =0A        10
LIST_            1CF      463
LOPINC           842      2114
LOPLMT           844      2116
LOPLN            846      2118
LOPPT            848      2120
LOPVAR           840      2112
LS1              1D8      472
LSTROM           7E0      2016
LT1              395      917
MD1              6A9      1705
MVDOWN           6A2      1698
MVUP             69A      1690
NEW              193      403
NEXT             2CB      715
NX0              2D2      722
NX1              30A      778
NX2              31D      797
NX3              2E8      744
NX4              2FA      762
NX5              31B      795
OC1             F5        245
OCSW             834      2100
OK              E6        230
PARN             47B      1147
PN0              632      1586
PN1              63D      1597
PN1A             646      1606
PN2              64C      1612
PN3              65A      1626
PN4              65B      1627
PN5              666      1638
PN6              66C      1644
PN6A             674      1652
PN7              67E      1662
PNBASE           835      2101
POPA             6AF      1711
PP1              6C8      1736
PR0              1FE      510
PR1              219      537
PR2              1F5      501
PR3              21F      543
PR5              205      517
PR6              227      551
PR8              22F      559
PRINT            1E6      486
PRTLN            683      1667
PRTNUM           623      1571
PRTSTG           5EA      1514
PS1              5EB      1515
PT0              4E8      1256
PT1              4F3      1267
PT2              4F4      1268
PU1              6EF      1775
PUSHA            6CA      1738
PUT              4E3      1251
QHOW            D0        208
QSORRY           580      1408
QT1              5FA      1530
QT1A             5FD      1533
QT2              603      1539
QT3              607      1543
QT4              60E      1550
QT5              617      1559
QT6              622      1570
QTSTG            5F5      1525
QWHAT            553      1363
RA1              4A0      1184
RAM              4D7      1239
RAMBGN         = 800      2048
RAMSZE         = 800      2048
RANPNT           84A      2122
RCOMP           20        32
REM              321      801
RETURN           257      599
REXPR           18        24
RFINISH         30        48
RIGNBLK         28        40
RND              486      1158
ROMBGN         =00        0
ROUTC           10        16
RTSTC           08        8
RTSTV           38        56
RUN              1A2      418
RUNNXL           1A8      424
RUNSML           1B8      440
RUNTSL           1B1      433
SETVAL           531      1329
SIZE             4BD      1213
SORRY           EF        239
ST2              131      305
ST3              13A      314
ST4              16E      366
STACK            900      2304
STKGOS           83A      2106
STKINP           83E      2110
STKLMT         = 84C      2124
STOP             19C      412
SUBDE            50D      1293
TAB1             727      1831
TAB2             737      1847
TAB4             789      1929
TAB5             7B4      1972
TAB6             7BA      1978
TAB8             7C2      1986
TC1             70        112
TC2             7A        122
TIBAS           D7        215
TN1             87        135
TOP              4DF      1247
TSTNUM          7E        126
TV1             5A        90
TV2             60        96
TX1             A9        169
TX2             BC        188
TXT              4DB      1243
TXTBGN           900      2304
TXTEND           F00      3840
TXTUNF           836      2102
USR              4D1      1233
USRSPC           F00      3840
VARBGN           800      2048
VARNXT           83C      2108
WHAT            E9        233
WSTART           11E      286
XP11             39C      924
XP12             3A2      930
XP13             3A8      936
XP14             3AF      943
XP15             3B7      951
XP16             3BD      957
XP17             3C3      963
XP18             3C5      965
XP21             3E2      994
XP22             3E5      997
XP23             3E8      1000
XP24             3EF      1007
XP25             400      1024
XP26             403      1027
XP31             40F      1039
XP32             42B      1067
XP33             432      1074
XP34             43B      1083
XP35             45A      1114
XP40             46D      1133
XP41             475      1141
XP42             482      1154
XP43             483      1155
